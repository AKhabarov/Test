////////////////////////////////////////////////////////////////////////////////
// Подсистема «Учет сохраняемого денежного содержания».
// 
// Клиентские процедуры и функции.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Процедура открывает общую форму, для просмотра и редактирования сохраняемого денежного содержания.
//
// Параметры:
//	Объект				- основной реквизит формы документа.
//	ИмяДокумента		- имя документа.
//	Владелец			- элемент, в который необходимо возвратить результат оповещения.
//	ОписаниеДокумента	- структура с описанием документа.
//
// Возвращаемое значение
//	Форма при закрытии отправляет оповещение владельцу, с которым передается содержимое данных для расчета сохраняемого
//	денежного содержания.
//
Процедура ОткрытьФормуВводаСохраняемогоДенежногоСодержания(Объект, ИмяДокумента, НазначениеРасчетаСтрока, Владелец, ОписаниеДокумента, ОповещениеЗавершения) Экспорт
	
	Если Не ПараметрыЗаполненыКорректно(Объект) Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Неопределено);
		КонецЕсли;	
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Владелец);
	ДополнительныеПараметры.Вставить("НазначениеРасчетаСтрока", НазначениеРасчетаСтрока);
	
	Если ОповещениеЗавершения <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуВводаСохраняемогоДенежногоСодержанияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Объект",		Объект);
	ПараметрыФормы.Вставить("ИмяДокумента",	ИмяДокумента);
	ПараметрыФормы.Вставить("НазначениеРасчетаСтрока", НазначениеРасчетаСтрока);
	ПараметрыФормы.Вставить("АдресПараметров", АдресДанныхДляРасчетаСохраняемогоДенежногоСодержания(Владелец, ОписаниеДокумента, НазначениеРасчетаСтрока));
	
	ОткрытьФорму("ОбщаяФорма.ВводДанныхДляРасчетаСохраняемогоДенежногоСодержания", ПараметрыФормы, Владелец, , , , Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция АдресДанныхДляРасчетаСохраняемогоДенежногоСодержания(Владелец, ОписаниеДокумента, НазначениеРасчетаСтрока)
	
	Объект = Владелец.Объект;
	
	Если НазначениеРасчетаСтрока ="ВыходноеПособие" Тогда
		СохраняемоеДенежноеСодержание = 0;
		МесячноеДенежноеСодержание = Объект.МесячноеДенежноеСодержание;
		ЕжемесячныеДопВыплатыСохраняются = Не Объект.ДолжностьЗамещаласьМенее12Месяцев;
	Иначе
		СохраняемоеДенежноеСодержание = Объект.СохраняемоеДенежноеСодержание;
		МесячноеДенежноеСодержание = 0;
		ЕжемесячныеДопВыплатыСохраняются = Ложь;
	КонецЕсли;
	
	ПараметрыДанных = Новый Структура;
	ПараметрыДанных.Вставить("ОписаниеДокумента", ОписаниеДокумента);
	ПараметрыДанных.Вставить("Организация", 							Объект.Организация);
	ПараметрыДанных.Вставить("Сотрудник", 								Объект.Сотрудник);
	ПараметрыДанных.Вставить("ПериодРасчета",							Объект[ОписаниеДокумента.МесяцНачисленияИмя]);
	ПараметрыДанных.Вставить("ДатаНачалаСобытия", 						Объект[ОписаниеДокумента.ДатаНачалаСобытияИмя]);
	ПараметрыДанных.Вставить("ПериодРасчетаСреднегоЗаработкаНачало", 	Объект.ПериодРасчетаСреднегоЗаработкаНачало);
	ПараметрыДанных.Вставить("ПериодРасчетаСреднегоЗаработкаОкончание", Объект.ПериодРасчетаСреднегоЗаработкаОкончание);
	ПараметрыДанных.Вставить("СохраняемоеДенежноеСодержание", 			СохраняемоеДенежноеСодержание);
	ПараметрыДанных.Вставить("МесячноеДенежноеСодержание", 				МесячноеДенежноеСодержание);
	ПараметрыДанных.Вставить("ДенежноеСодержание", 						Объект.ДенежноеСодержание);
	ПараметрыДанных.Вставить("ДенежноеСодержаниеФактическиеНачисления", Объект.ДенежноеСодержаниеФактическиеНачисления);
	ПараметрыДанных.Вставить("ЕжемесячныеДопВыплатыСохраняются", 		ЕжемесячныеДопВыплатыСохраняются);
	
	Возврат ПоместитьВоВременноеХранилище(ПараметрыДанных, Владелец.УникальныйИдентификатор);
	
КонецФункции

Процедура ОткрытьФормуВводаСохраняемогоДенежногоСодержанияЗавершение(АдресРезультата, ДополнительныеПараметры) Экспорт
	
	Если АдресРезультата <> Неопределено Тогда
		
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		
		ТЧДенежноеСодержание = ДополнительныеПараметры.Форма.Объект.ДенежноеСодержание;
		НазначениеРасчета = Результат.НазначениеРасчета;
		СтрокиКУдалению = Новый Массив;
		Для каждого Строка Из ТЧДенежноеСодержание Цикл
			Если Строка.НазначениеРасчета = НазначениеРасчета Тогда
				СтрокиКУдалению.Добавить(Строка);
			КонецЕсли;
		КонецЦикла;
		Для каждого Строка Из СтрокиКУдалению Цикл
			ТЧДенежноеСодержание.Удалить(Строка);
		КонецЦикла;
		
		Для каждого ЗаписьДенежноеСодержание Из Результат.ДенежноеСодержание Цикл
			ЗаполнитьЗначенияСвойств(ТЧДенежноеСодержание.Добавить(), ЗаписьДенежноеСодержание);
		КонецЦикла;
		
		ТЧДенежноеСодержаниеФактическиеНачисления = ДополнительныеПараметры.Форма.Объект.ДенежноеСодержаниеФактическиеНачисления;
		СтрокиКУдалению = Новый Массив;
		Для каждого Строка Из ТЧДенежноеСодержаниеФактическиеНачисления Цикл
			Если Строка.НазначениеРасчета = НазначениеРасчета Тогда
				СтрокиКУдалению.Добавить(Строка);
			КонецЕсли;
		КонецЦикла;
		Для каждого Строка Из СтрокиКУдалению Цикл
			ТЧДенежноеСодержаниеФактическиеНачисления.Удалить(Строка);
		КонецЦикла;
		
		Для каждого ЗаписьДенежноеСодержаниеФактическиеНачисления Из Результат.ДенежноеСодержаниеФактическиеНачисления Цикл
			ЗаполнитьЗначенияСвойств(ТЧДенежноеСодержаниеФактическиеНачисления.Добавить(), ЗаписьДенежноеСодержаниеФактическиеНачисления);
		КонецЦикла;
		
		Если ДополнительныеПараметры.НазначениеРасчетаСтрока = "ВыходноеПособие" Тогда
			ДополнительныеПараметры.Форма.Объект.МесячноеДенежноеСодержание = Результат.СохраняемоеДенежноеСодержание;
		Иначе
			ДополнительныеПараметры.Форма.Объект.СохраняемоеДенежноеСодержание = Результат.СохраняемоеДенежноеСодержание;
		КонецЕсли;
		
		ДополнительныеПараметры.Форма.Модифицированность = Истина;
		
	КонецЕсли; 
	
	Если ДополнительныеПараметры.Свойство("ОповещениеЗавершения") Тогда
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, АдресРезультата);
		
	КонецЕсли; 
	
КонецПроцедуры

Функция ПараметрыЗаполненыКорректно(Объект)
	
	Если Не ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Сотрудник не указан.'"));
		Возврат Ложь;
	КонецЕсли;	
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
