////////////////////////////////////////////////////////////////////////////////
// Подсистема "Тарификационная отчетность в медицине".
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьЗначенияРеквизитовПоказателяТарификации(Форма) Экспорт
	
	Если Не Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьТарификационнуюОтчетностьУчрежденийФМБА") Тогда
		Возврат;
	КонецЕсли;

	Объект = Форма.Объект;
	
	ЧисловойЗависящийОтСтажа = Объект.ТипПоказателя = ПредопределенноеЗначение("Перечисление.ТипыПоказателейРасчетаЗарплаты.ЧисловойЗависящийОтСтажа");
	
	ПостоянныйЧисловой = (Объект.ТипПоказателя = ПредопределенноеЗначение("Перечисление.ТипыПоказателейРасчетаЗарплаты.Числовой")
		И Объект.СпособПримененияЗначений = ПредопределенноеЗначение("Перечисление.СпособыПримененияЗначенийПоказателейРасчетаЗарплаты.Постоянное"));
		
	Если Не ЧисловойЗависящийОтСтажа И Не ПостоянныйЧисловой Тогда 
		Форма.ЯвляетсяПоказателемТарификации = Ложь;
	КонецЕсли;
	
	Если Форма.ЯвляетсяПоказателемТарификации И ЧисловойЗависящийОтСтажа Тогда
		Форма.ВидПоказателяТарификации = ПредопределенноеЗначение("Перечисление.ПоказателиТарификацииМедРаботников.ПроцентНадбавкиЗаВыслугуЛет");
	ИначеЕсли Не (ЧисловойЗависящийОтСтажа ИЛИ ПостоянныйЧисловой) Или Не Форма.ЯвляетсяПоказателемТарификации Тогда 
		Форма.ВидПоказателяТарификации = Неопределено;
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ВидПоказателяТарификацииГруппа", "Доступность", (ЧисловойЗависящийОтСтажа ИЛИ ПостоянныйЧисловой));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ВидПоказателяТарификации", "Доступность", Форма.ЯвляетсяПоказателемТарификации);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ВидПоказателяТарификации", "АвтоОтметкаНезаполненного", Форма.ЯвляетсяПоказателемТарификации); 
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ВидПоказателяТарификации", "ОтметкаНезаполненного",     Форма.ЯвляетсяПоказателемТарификации И Не ЗначениеЗаполнено(Форма.ВидПоказателяТарификации));

КонецПроцедуры

#КонецОбласти