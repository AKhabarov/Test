
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обмен ЗГУ 3.0 и БГУ 2.0"
// Серверные процедуры и функции, обслуживающие подписки на события.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьНастройкиИспользования(Источник, Отказ) Экспорт
	
	Если Метаданные.ПодпискиНаСобытия.ЗаписатьНастройкиИспользованияОбменаЗГУБГУ2.Источник.Типы().Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("Загрузка") Или Источник.ДополнительныеСвойства.Свойство("ПолучениеСообщенияОбмена") Тогда
		Возврат;
	КонецЕсли;
	
	ИсточникСсылка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка, "НомерОтправленного, НомерПринятого");
	
	Если ИсточникСсылка.НомерОтправленного <> Источник.НомерОтправленного Или ИсточникСсылка.НомерПринятого <> Источник.НомерПринятого Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеПланаОбмена = Источник.Метаданные();
	
	Если МетаданныеПланаОбмена.Реквизиты.Найти("ИспользоватьОтборПоОрганизациям") = Неопределено
		Или МетаданныеПланаОбмена.ТабличныеЧасти.Найти("Организации") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПланаОбмена = МетаданныеПланаОбмена.Имя;
	ПолноеИмяПланаОбмена = МетаданныеПланаОбмена.ПолноеИмя();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена[ИмяПланаОбмена].ЭтотУзел());
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОбменЗарплатаБухгалтерия.ИспользоватьОтборПоОрганизациям
	|ИЗ
	|	#ПолноеИмяПланаОбмена КАК ОбменЗарплатаБухгалтерия
	|ГДЕ
	|	ОбменЗарплатаБухгалтерия.Ссылка <> &ЭтотУзел";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ПолноеИмяПланаОбмена", ПолноеИмяПланаОбмена);
	
	ЕстьОбмены = Не Запрос.Выполнить().Пустой();
	Если Не ЕстьОбмены Тогда
		Константы.ИспользоватьОбменЗГУБГУ2ПоВсемОрганизациям.Установить(Ложь);
		НаборЗаписей = РегистрыСведений.ИспользованиеОбменаЗГУБГУ2ПоОрганизациям.СоздатьНаборЗаписей();
		НаборЗаписей.Записать();
		Возврат;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ОбменЗарплатаБухгалтерия.ИспользоватьОтборПоОрганизациям) КАК ИспользоватьОтборПоОрганизациям
	|ИЗ
	|	#ПолноеИмяПланаОбмена КАК ОбменЗарплатаБухгалтерия
	|ГДЕ
	|	ОбменЗарплатаБухгалтерия.Ссылка <> &ЭтотУзел";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ПолноеИмяПланаОбмена", ПолноеИмяПланаОбмена);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Если Не Выборка.ИспользоватьОтборПоОрганизациям Тогда
		Константы.ИспользоватьОбменЗГУБГУ2ПоВсемОрганизациям.Установить(Истина);
		НаборЗаписей = РегистрыСведений.ИспользованиеОбменаЗГУБГУ2ПоОрганизациям.СоздатьНаборЗаписей();
		НаборЗаписей.Записать();
		Возврат;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбменЗарплатаБухгалтерияОрганизации.Организация,
	|	ИСТИНА КАК Используется
	|ИЗ
	|	#ПолноеИмяПланаОбменаТаблицаОрганизации КАК ОбменЗарплатаБухгалтерияОрганизации
	|ГДЕ
	|	ОбменЗарплатаБухгалтерияОрганизации.Ссылка <> &ЭтотУзел";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ПолноеИмяПланаОбменаТаблицаОрганизации", ПолноеИмяПланаОбмена + ".Организации");
	НаборЗаписей = РегистрыСведений.ИспользованиеОбменаЗГУБГУ2ПоОрганизациям.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ЗарегистрироватьИзменение(Источник, Отказ) Экспорт
	
	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписью("ОбменЗГУБГУ2", Источник, Отказ);
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзменениеДокумента(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписьюДокумента("ОбменЗГУБГУ2", Источник, Отказ, РежимЗаписи, РежимПроведения);
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзменениеНабораЗаписей(Источник, Отказ, Замещение) Экспорт
	
	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписьюРегистра("ОбменЗГУБГУ2", Источник, Отказ, Замещение);
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьУдаление(Источник, Отказ) Экспорт
	
	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередУдалением("ОбменЗГУБГУ2", Источник, Отказ);
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
