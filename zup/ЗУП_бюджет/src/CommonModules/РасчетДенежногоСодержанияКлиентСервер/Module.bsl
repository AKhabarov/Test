////////////////////////////////////////////////////////////////////////////////
// Подсистема «Учет сохраняемого денежного содержания».
// 
// Процедуры и функции, предназначенные для использования как на сервере, 
// так и на клиенте.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция ОписаниеДанныеДокументаДляРасчетаСохраняемогоДенежногоСодержания() Экспорт

	ДанныеДокумента = Новый Структура(
	"ДанныеОНачислениях,
	|ДанныеОФактическихНачислениях,
	|ДанныеОбИндексации,
	|ПериодРасчетаНачало,
	|ПериодРасчетаОкончание,
	|СотрудникиДокумента,
	|ДокументСсылка,
	|НазначенияРасчетаДенежногоСодержания,
	|Организация,
	|ПериодРегистрации,
	|ДолжностьЗамещаласьМенее12Месяцев,
	|ДатаНачалаСобытия");
	
	ДанныеДокумента.ДолжностьЗамещаласьМенее12Месяцев = Ложь;
	
	Возврат ДанныеДокумента;

КонецФункции

Функция ОписаниеПараметровСохраняемоеДенежноеСодержание() Экспорт

	Параметры = Новый Структура(
	"Организация,
	|ПериодРасчета,
	|НазначениеРасчета,
	|ДатаНачалаСобытия,
	|ДанныеОНачислениях,
	|ДанныеОФактическихНачислениях,
	|НазначенияРасчетаДенежногоСодержания,
	|Сотрудник");

	Возврат Параметры;
	
КонецФункции 

Процедура ЗаполнитьИнформациюЗаполненностиДанныхСохраняемоеДенежноеСодержаниеДокумента(Форма, ОписаниеДокумента, СтруктураИнфонадписи) Экспорт
	
	ЕстьНеЗаполненныеДанные = Ложь;
	
	Если Форма.Объект.ДенежноеСодержание.Количество() = 0 Тогда
		ЕстьНеЗаполненныеДанные = Истина;
	КонецЕсли;
	
	ТекстСообщения = "";
	Если ЕстьНеЗаполненныеДанные Тогда
		ТекстСообщения = НСтр("ru = 'Данные о сохраняемом денежном содержании неполные.
		|Для ввода недостающих данных используйте команду ""Изменить"".'");
	Иначе
		
		ДатаСобытия = Форма.Объект[ОписаниеДокумента.ДатаНачалаСобытияИмя];
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Данные о сохраняемом денежном содержании на %1.'"), 
			Формат(ДатаСобытия, "ДЛФ=D"));
		Если Форма.РасчетДенежногоСодержанияРедактировался Тогда
			ТекстСообщения = ТекстСообщения + НСтр("ru = ' Есть ручные корректировки!'");
		КонецЕсли;
	
	КонецЕсли;
	
	СтруктураИнфонадписи.Текст = Новый ФорматированнаяСтрока(ТекстСообщения);
	СтруктураИнфонадписи.Картинка = ?(ЕстьНеЗаполненныеДанные, БиблиотекаКартинок.Предупреждение, БиблиотекаКартинок.Информация);
	
КонецПроцедуры

#КонецОбласти
