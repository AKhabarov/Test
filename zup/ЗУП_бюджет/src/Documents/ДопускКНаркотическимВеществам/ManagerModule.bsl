#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаДокумента.
Функция ОписаниеСоставаДокумента() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ДопускКНаркотическимВеществам;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаДокументаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
			
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
		КомандаПечати.Идентификатор = "ПФ_MXL_ПриказОДопуске";
		КомандаПечати.Представление = НСтр("ru = 'Приказ о допуске'");
		КомандаПечати.Порядок = 10;
			
КонецПроцедуры
	
// Формирует печатные формы
//
// Параметры:
//  (входные)
//    МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//    ПараметрыПечати - Структура - дополнительные настройки печати;
//  (выходные)
//   КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы.
//   ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                             представление - имя области в которой был выведен объект;
//   ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ПриказОДопуске") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,	"ПФ_MXL_ПриказОДопуске", 
																НСтр("ru = 'Приказ о допуске'"), ПечатьПриказа(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;	
					
КонецПроцедуры

Функция ПечатьПриказа(МассивОбъектов, ОбъектыПечати)
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Приказа_о_допуске";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ДопускКНаркотическимВеществам.ПФ_MXL_ПриказОДопуске");
	
	ДанныеПечатиОбъектов = ДанныеПечатиДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;	
	
	Для Каждого ДанныеПечати Из ДанныеПечатиОбъектов Цикл
		
		ДанныеДокумента = ДанныеПечати.Значение;
		Макет.Параметры.Заполнить(ДанныеДокумента);
		
		Для каждого ДанныеСотрудника Из ДанныеДокумента.ДанныеСотрудников Цикл
			
			// Документы нужно выводить на разных страницах.
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;	
			
			ПервыйДокумент = Ложь;
			
			// Запомним номер строки, с которой начали выводить текущий документ.
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Макет.Параметры.Заполнить(ДанныеСотрудника);
			
			Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений И ЗначениеЗаполнено(Макет.Параметры.Подразделение) Тогда
				Макет.Параметры.Подразделение = Макет.Параметры.Подразделение.ПолноеНаименование();
			КонецЕсли;
		
			ТабличныйДокумент.Вывести(Макет);
			
			// В табличном документе необходимо задать имя области, в которую был 
			// выведен объект. Нужно для возможности печати покомплектно.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеСотрудника.Ссылка); 
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции 

Функция ДанныеПечатиДокументов(МассивОбъектов)
	
	ДанныеПечатиОбъектов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст ="ВЫБРАТЬ
	              |	ДопускКНаркотическимВеществам.Ссылка.Номер КАК Номер,
	              |	ДопускКНаркотическимВеществам.Ссылка.Дата КАК Дата,
	              |	ДопускКНаркотическимВеществам.Ссылка.Дата КАК Период,
	              |	ДопускКНаркотическимВеществам.Ссылка.Организация КАК Организация,
	              |	ДопускКНаркотическимВеществам.Сотрудник,
	              |	ДопускКНаркотическимВеществам.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	              |	ДопускКНаркотическимВеществам.Ссылка.Руководитель КАК Руководитель,
	              |	ДопускКНаркотическимВеществам.Ссылка.ДолжностьРуководителя КАК ДолжностьРуководителя,
	              |	ДопускКНаркотическимВеществам.Ссылка,
	              |	ДопускКНаркотическимВеществам.СправкаНаркологическогоДиспансера,
	              |	ДопускКНаркотическимВеществам.СправкаПсихоневрологическогоДиспансера,
	              |	ДопускКНаркотическимВеществам.Ссылка.ДокументОснование КАК ДокументОснование
	              |ПОМЕСТИТЬ ВТДанныеДокументов
	              |ИЗ
	              |	Документ.ДопускКНаркотическимВеществам.Сотрудники КАК ДопускКНаркотическимВеществам
	              |ГДЕ
	              |	ДопускКНаркотическимВеществам.Ссылка В(&МассивОбъектов)"; 

	Запрос.Выполнить();
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		"ВТДанныеДокументов");
		
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(
		ОписательВременныхТаблиц,
		Истина,
		"ФИОПолные,ТабельныйНомер,Подразделение,Должность");
	
	ИменаПолейОтветственныхЛиц = Новый Массив;
	ИменаПолейОтветственныхЛиц.Добавить("Руководитель");
	
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Ложь, ИменаПолейОтветственныхЛиц, "ВТДанныеДокументов");
			
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДокументов.Ссылка,
		|	ДанныеДокументов.Номер КАК НомерДокумента,
		|	ДанныеДокументов.Период КАК ДатаДокумента,
		|	ДанныеДокументов.Организация.КодПоОКПО КАК КодПоОКПО,
		|	ВЫРАЗИТЬ(ДанныеДокументов.Организация.НаименованиеПолное КАК СТРОКА(200)) КАК НазваниеОрганизации,
		|	ДанныеДокументов.ДолжностьРуководителя,
		|	ФИООтветственныхЛиц.РасшифровкаПодписи КАК ФИОРуководителя,
		|	ДанныеДокументов.ДокументОснование.НомерОтвета КАК НомерОтвета,
		|	ДанныеДокументов.ДокументОснование.ДатаОтвета КАК ДатаОтвета,
		|	ДанныеДокументов.ДокументОснование.Номер КАК НомерЗапроса,
		|	ДанныеДокументов.ДокументОснование.Дата КАК ДатаЗапроса,
		|	ДанныеДокументов.ДокументОснование.ОрганФСКН КАК ОрганФСКН
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИООтветственныхЛиц
		|		ПО ДанныеДокументов.Руководитель = ФИООтветственныхЛиц.ФизическоеЛицо
		|			И ДанныеДокументов.Ссылка = ФИООтветственныхЛиц.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокументов.Ссылка,
		|	ЕСТЬNULL(КадровыеДанныеСотрудников.ФИОПолные, ДанныеДокументов.Сотрудник.ФизическоеЛицо) КАК ФИО,
		|	КадровыеДанныеСотрудников.ТабельныйНомер,
		|	КадровыеДанныеСотрудников.Подразделение,
		|	КадровыеДанныеСотрудников.Должность,
		|	ДанныеДокументов.СправкаНаркологическогоДиспансера.НомерСправки КАК НомерСправкиНД,
		|	ДанныеДокументов.СправкаНаркологическогоДиспансера.ДатаС КАК ДатаСправкиНД,
		|	ДанныеДокументов.СправкаПсихоневрологическогоДиспансера.НомерСправки КАК НомерСправкиПД,
		|	ДанныеДокументов.СправкаПсихоневрологическогоДиспансера.ДатаС КАК ДатаСправкиПД
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|		ПО ДанныеДокументов.Период = КадровыеДанныеСотрудников.Период
		|			И ДанныеДокументов.Сотрудник = КадровыеДанныеСотрудников.Сотрудник"; 

	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ДокументыДляПечати = РезультатыЗапроса[0].Выгрузить();
 	СтрокиДокументов = РезультатыЗапроса[1].Выгрузить();

	Для Каждого ДокументДляПечати Из ДокументыДляПечати Цикл
		
		ДанныеПечати = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ДокументДляПечати);
		
		ДанныеСотрудников = СтрокиДокументов.НайтиСтроки(Новый Структура("Ссылка", ДанныеПечати.Ссылка));
		ДанныеПечати.Вставить("ДанныеСотрудников", ДанныеСотрудников);
		
		// Заполнение соответствия
		ДанныеПечатиОбъектов.Вставить(ДокументДляПечати.Ссылка, ДанныеПечати);

	КонецЦикла;
	
	Возврат ДанныеПечатиОбъектов;
	
КонецФункции

#КонецОбласти

#КонецОбласти
	
#КонецЕсли