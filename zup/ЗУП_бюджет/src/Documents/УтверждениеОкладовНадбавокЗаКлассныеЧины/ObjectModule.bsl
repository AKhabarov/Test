
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Проведение документа
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	КлассныеЧиныРанги.СформироватьДвиженияОкладовНадбавокПоКласснымЧинамРангам(Движения, ДанныеДляПроведения.ОкладыНадбавкиПоКласснымЧинамРангам);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаВступленияВСилу, "Объект.ДатаВступленияВСилу", Отказ, НСтр("ru='Дата вступления в силу'"), , , Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	УтверждениеОкладовНадбавокЗаКлассныеЧиныОкладыНадбавки.Ссылка.ДатаВступленияВСилу КАК Период,
	               |	УтверждениеОкладовНадбавокЗаКлассныеЧиныОкладыНадбавки.КлассныйЧинРанг,
	               |	УтверждениеОкладовНадбавокЗаКлассныеЧиныОкладыНадбавки.Размер
	               |ИЗ
	               |	Документ.УтверждениеОкладовНадбавокЗаКлассныеЧины.ОкладыНадбавки КАК УтверждениеОкладовНадбавокЗаКлассныеЧиныОкладыНадбавки
	               |ГДЕ
	               |	УтверждениеОкладовНадбавокЗаКлассныеЧиныОкладыНадбавки.Ссылка = &Ссылка";
				   
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеДляПроведения = Новый Структура;
	
	ОкладыНадбавкиПоКласснымЧинамРангам = РезультатЗапроса.Выгрузить();
	ДанныеДляПроведения.Вставить("ОкладыНадбавкиПоКласснымЧинамРангам", ОкладыНадбавкиПоКласснымЧинамРангам);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#КонецОбласти

#КонецЕсли
