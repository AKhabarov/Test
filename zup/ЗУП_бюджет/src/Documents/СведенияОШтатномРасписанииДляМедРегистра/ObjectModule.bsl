#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	
	ПозицииДокумента = ШтатныеЕдиницы.ВыгрузитьКолонку("ПозицияШтатногоРасписания");
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоПозициямШтатногоРасписания(ЭтотОбъект, ПозицииДокумента, Организация);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет документ данными из хранилища.
// Параметры: АдресХранилища - адрес временного хранилища.
Процедура ЗаполнитьИзХранилища(АдресХранилища) Экспорт

	ПараметрыЗаполнения = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	СписокПозиций = ПараметрыЗаполнения.СписокПозицийШтатногоРасписания;
	ДанныеДляЗаполнения = ПараметрыЗаполнения.ДанныеДляЗаполнения;
	
	Если ТипЗнч(ДанныеДляЗаполнения) <> Тип("ТаблицаЗначений") Тогда
		ДанныеДляЗаполнения = ТаблицаИзМассива(ДанныеДляЗаполнения);
	КонецЕсли;
	
	// 1 Скопом весь документ
	Если СписокПозиций=Неопределено Или СписокПозиций.Количество() = 0 Тогда
		
		ЭтотОбъект.ШтатныеЕдиницы.Загрузить(ДанныеДляЗаполнения);
		Возврат;
		
	КонецЕсли;
	
	// 2 Выбранным списком
	Для Каждого Позиция Из СписокПозиций Цикл
		
		СтрокаШтатнойЕдиницы = ШтатныеЕдиницы.Найти(Позиция, "ПозицияШтатногоРасписания");
		Если СтрокаШтатнойЕдиницы = Неопределено Тогда
			СтрокаШтатнойЕдиницы = ШтатныеЕдиницы.Добавить();
		КонецЕсли;
		
		СтрокаДляЗаполнения = ДанныеДляЗаполнения.Найти(Позиция, "ПозицияШтатногоРасписания");
		Если СтрокаДляЗаполнения = Неопределено Тогда
			ШтатныеЕдиницы.Удалить(СтрокаШтатнойЕдиницы);
		Иначе
			ЗаполнитьЗначенияСвойств(СтрокаШтатнойЕдиницы, СтрокаДляЗаполнения);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Функция ТаблицаИзМассива(МассивЗначений)
	
	Таблица = ШтатныеЕдиницы.Выгрузить(Новый Структура("НомерСтроки", ""));
	
	Для Каждого Элемент Из МассивЗначений Цикл
		ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Элемент);
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции	

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ПустаяСтрока(УИНОрганизации) Или УИНОрганизации = "........." Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Идентификатор учреждения должен быть заполнен'"), ЭтотОбъект, "УИНОрганизации", , Отказ);
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "УИНОрганизации");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли