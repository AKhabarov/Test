
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Если Не ЗначениеЗаполнено(Объект.ДатаВступленияВСилу) Тогда
			Объект.ДатаВступленияВСилу = ДобавитьМесяц(НачалоМесяца(ТекущаяДатаСеанса()), 1);
		КонецЕсли;
		
		ЗначенияДляЗаполнения = Новый Структура;
		ЗначенияДляЗаполнения.Вставить("Ответственный", "Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВоинскиеСпециальныеЗвания.Ссылка
		               |ИЗ
		               |	Справочник.ВоинскиеСпециальныеЗвания КАК ВоинскиеСпециальныеЗвания
		               |ГДЕ
		               |	НЕ ВоинскиеСпециальныеЗвания.ВАрхиве
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ВоинскиеСпециальныеЗвания.Наименование";
					   
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			НоваяСтрока = Объект.Оклады.Добавить();
			НоваяСтрока.ВоинскоеСпециальноеЗвание = Выборка.Ссылка;
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьДействующиеЗначенияОкладов();
	УстановитьТочностьРазмераОклада();
	
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_УтверждениеОкладовЗаВоинскиеСпециальныеЗвания", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаВступленияВСилуПриИзменении(Элемент)
	
	ЗаполнитьДействующиеЗначенияОкладов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОклады

&НаКлиенте
Процедура ОкладыВоинскоеСпециальноеЗваниеПриИзменении(Элемент)
	
	ЗаполнитьДействующееЗначениеОкладаВСтроке();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДействующиеЗначенияОкладов()
	
	ИзмеренияДаты = ВоинскиеСпециальныеЗвания.ИсходныеДанныеДляОпределенияОкладовПоВоинскомуСпециальномуЗванию();
	
	Для Каждого СтрокаОклада Из Объект.Оклады Цикл 
		НоваяСтрока = ИзмеренияДаты.Добавить();
		НоваяСтрока.ВоинскоеСпециальноеЗвание = СтрокаОклада.ВоинскоеСпециальноеЗвание;
		НоваяСтрока.Период = Объект.ДатаВступленияВСилу;
	КонецЦикла;
	
	ЗначенияОкладов = ВоинскиеСпециальныеЗвания.ЗначенияДействующихОкладовПоВоинскимСпециальнымЗваниям(ИзмеренияДаты, Объект.Ссылка);
	
	Для Каждого СтрокаОклада Из Объект.Оклады Цикл
		Отбор = Новый Структура("Период, ВоинскоеСпециальноеЗвание", Объект.ДатаВступленияВСилу, СтрокаОклада.ВоинскоеСпециальноеЗвание);
		НайденыеСтроки = ЗначенияОкладов.НайтиСтроки(Отбор);
		СтрокаОклада.ТекущийРазмер = ?(НайденыеСтроки.Количество() > 0, НайденыеСтроки[0].Размер, Неопределено);
		СтрокаОклада.ТекущийРазмерПредставление = ПредставлениеДействующегоРазмераОклада(СтрокаОклада.ТекущийРазмер);
	КонецЦикла;	
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ОкладыТекущийРазмерПредставление", "Видимость", ЗначенияОкладов.Количество() > 0);
		
	Если ЗначенияОкладов.Количество() > 0 Тогда 
		Объект.Оклады.Сортировать("ТекущийРазмер Убыв");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДействующееЗначениеОкладаВСтроке()
	
	ИдентификаторСтроки = Элементы.Оклады.ТекущаяСтрока;
	СтрокаОклада = Объект.Оклады.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	СтрокаОклада.ТекущийРазмер = ВоинскиеСпециальныеЗвания.ЗначениеОкладаПоВоинскомуСпециальномуЗванию(СтрокаОклада.ВоинскоеСпециальноеЗвание, Объект.ДатаВступленияВСилу, Объект.Ссылка);
	СтрокаОклада.ТекущийРазмерПредставление = ПредставлениеДействующегоРазмераОклада(СтрокаОклада.ТекущийРазмер);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеДействующегоРазмераОклада(ТекущийРазмер)
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'было: %1'"), Формат(ТекущийРазмер, "ЧДЦ=2"));
	
КонецФункции

&НаСервере
Процедура УстановитьТочностьРазмераОклада()
	
	ИспользоватьРасчетДенежногоДовольствияВоеннослужащих = ПолучитьФункциональнуюОпцию("ИспользоватьРасчетДенежногоДовольствияВоеннослужащих");
		
	Показатель = ВоинскиеСпециальныеЗвания.ПоказательОкладПоВоинскомуСпециальномуЗванию();
		
	СведенияОПоказателе = ЗарплатаКадрыРасширенный.СведенияОПоказателеРасчетаЗарплаты(Показатель);	
	ФорматЗначенияПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ЧДЦ=%1", СведенияОПоказателе.Точность);
	
	Элементы.ОкладыРазмер.Формат = ФорматЗначенияПоказателя;
	Элементы.ОкладыРазмер.ФорматРедактирования = ФорматЗначенияПоказателя;
	
КонецПроцедуры

#КонецОбласти
