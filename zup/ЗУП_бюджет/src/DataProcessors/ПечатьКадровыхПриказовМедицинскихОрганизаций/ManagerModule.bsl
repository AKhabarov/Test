#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции	

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ОтчетОПрофессиональнойДеятельности") Тогда
		ТабличныйДокумент = ТабличныйДокументОтчетОПрофессиональнойДеятельности(МассивОбъектов, ОбъектыПечати);
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,	"ПФ_MXL_ОтчетОПрофессиональнойДеятельности", НСтр("ru = 'Отчет о профессиональной деятельности (титульный лист)'"), ТабличныйДокумент);
		КонецЕсли;
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ЗаявлениеОПодтвержденииКвалификации") Тогда
		ТабличныйДокумент = ТабличныйДокументЗаявлениеОПодтвержденииКвалификации(МассивОбъектов, ОбъектыПечати);
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,	"ПФ_MXL_ЗаявлениеОПодтвержденииКвалификации", НСтр("ru = 'Заявление на подтверждение квалификации по специальности'"), ТабличныйДокумент);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область ОтчетОПрофессиональнойДеятельности

Функция ТабличныйДокументОтчетОПрофессиональнойДеятельности(ПриказыОбАттестации, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб 			= Истина;
	ДокументРезультат.КлючПараметровПечати 	= "ПараметрыПечати_АттестационныйЛистМедицинскогоРаботника";

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьВТСотрудникиПриказовОбАттестации(МенеджерВременныхТаблиц, ПриказыОбАттестации);

	ДанныеДляПечати = ДанныеДляПечатиОтчетаОПрофессиональнойДеятельности(МенеджерВременныхТаблиц, ПриказыОбАттестации);
	
	Пока ДанныеДляПечати.Следующий() Цикл

		Если ДокументРезультат.ВысотаТаблицы > 1 Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
		НомерПоПорядку 	= 1;
		
		ВывестиОтчетОПрофессиональнойДеятельности(ДокументРезультат, ДанныеДляПечати);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НомерСтрокиНачало, ОбъектыПечати, ДанныеДляПечати.Ссылка);
		
	КонецЦикла;	
	
	Возврат ДокументРезультат;
	
КонецФункции

Процедура ВывестиОтчетОПрофессиональнойДеятельности(ДокументРезультат, ДанныеДляПечати)
	 		  
	ОбластиМакета = ОбластиМакетаОтчетОПрофессиональнойДеятельности();
	ТитульныйЛист = ОбластиМакета.ТитульныйЛист;
	
	ДанныеТитульногоЛиста = Новый Структура;
	ДанныеТитульногоЛиста.Вставить("ФИОРуководителя", 				ДанныеДляПечати.ФИОРуководителя);
	ДанныеТитульногоЛиста.Вставить("Годы", 							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 - %2", Формат(ДанныеДляПечати.НачалоОтчета, "ДФ=yyyy"), Формат(ДанныеДляПечати.КонецОтчета, "ДФ=yyyy")));
	ДанныеТитульногоЛиста.Вставить("ФИОСотрудника", 				ДанныеДляПечати.ФИОСотрудника);
	ДанныеТитульногоЛиста.Вставить("Должность", 					ДанныеДляПечати.Должность);
	ДанныеТитульногоЛиста.Вставить("НаименованиеОрганизации", 		?(ЗначениеЗаполнено(ДанныеДляПечати.НаименованиеПолное), ДанныеДляПечати.НаименованиеПолное, ДанныеДляПечати.НаименованиеСокращенное));
	ДанныеТитульногоЛиста.Вставить("ПодтверждаемаяСпециальность", 	ДанныеДляПечати.ПодтверждаемаяСпециальность);
	
	ЗаполнитьЗначенияСвойств(ТитульныйЛист.Параметры, ДанныеТитульногоЛиста);
	
	ДокументРезультат.Вывести(ТитульныйЛист);
	
КонецПроцедуры
	 
Функция ОбластиМакетаОтчетОПрофессиональнойДеятельности()
	
	ОбластиМакета = Новый Структура;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьКадровыхПриказовМедицинскихОрганизаций.ПФ_MXL_ОтчетОПрофессиональнойДеятельности");
	ОбластиМакета.Вставить("ТитульныйЛист", Макет.ПолучитьОбласть("ТитульныйЛист"));
	
	Возврат ОбластиМакета;
	
КонецФункции

Функция ДанныеДляПечатиОтчетаОПрофессиональнойДеятельности(МенеджерВременныхТаблиц, ПриказыОбАттестации)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПриказыОбАттестации", ПриказыОбАттестации);
	Запрос.УстановитьПараметр("ВидАттестации", Медицина.ВидАттестацииМедицинскихРаботников());
	
	НеобходимыеКадровыеДанные = "ФИОПолные,Должность";
	СоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, НеобходимыеКадровыеДанные);
	СоздатьВТКадровыеДанныеОтветственныхЛицОрганизации(Запрос.МенеджерВременныхТаблиц, ПриказыОбАттестации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриказОбАттестацииСотрудники.Ссылка КАК Ссылка,
	|	ПриказОбАттестацииСотрудники.Сотрудник КАК Сотрудник,
	|	ПриказОбАттестацииСотрудники.Специальность КАК ПодтверждаемаяСпециальность,
	|	Организации.НаименованиеПолное,
	|	Организации.НаименованиеСокращенное,
	|	ВТКадровыеДанныеРуководителей.ИОФамилия КАК ФИОРуководителя,
	|	ВТКадровыеДанныеСотрудников.Должность,
	|	ВТКадровыеДанныеСотрудников.ФИОПолные КАК ФИОСотрудника,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания, ГОД), ГОД, -3) КАК НачалоОтчета,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания, ГОД), ГОД, -1) КАК КонецОтчета,
	|	ПриказОбАттестации.ВидАттестации
	|ИЗ
	|	Документ.ПриказОбАттестацииСотрудников КАК ПриказОбАттестации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриказОбАттестацииСотрудников.Сотрудники КАК ПриказОбАттестацииСотрудники
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриказОбАттестацииСотрудников.ЗаседанияКомиссии КАК ПриказОбАттестацииЗаседанияКомиссии
	|			ПО ПриказОбАттестацииСотрудники.Ссылка = ПриказОбАттестацииЗаседанияКомиссии.Ссылка
	|				И ПриказОбАттестацииСотрудники.ИдентификаторСтрокиЗаседаниеКомиссии = ПриказОбАттестацииЗаседанияКомиссии.ИдентификаторСтрокиЗаседаниеКомиссии
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|			ПО ПриказОбАттестацииСотрудники.Сотрудник = ВТКадровыеДанныеСотрудников.Сотрудник
	|				И (ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания = ВТКадровыеДанныеСотрудников.Период)
	|		ПО (ПриказОбАттестацииСотрудники.Ссылка = ПриказОбАттестации.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ПриказОбАттестации.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеОтветственныхЛицОрганизации КАК ВТКадровыеДанныеРуководителей
	|		ПО ПриказОбАттестации.Руководитель = ВТКадровыеДанныеРуководителей.ФизическоеЛицо
	|			И (ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания = ВТКадровыеДанныеРуководителей.Период)
	|ГДЕ
	|	ПриказОбАттестации.Ссылка В(&ПриказыОбАттестации)
	|	И ПриказОбАттестации.ВидАттестации = &ВидАттестации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Сотрудник,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Специальность,
	|	Организации.НаименованиеПолное,
	|	Организации.НаименованиеСокращенное,
	|	ВТКадровыеДанныеРуководителей.ИОФамилия,
	|	ВТКадровыеДанныеСотрудников.Должность,
	|	ВТКадровыеДанныеСотрудников.ФИОПолные,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации, ГОД), ГОД, -3),
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации, ГОД), ГОД, -1),
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.ВидАттестации
	|ИЗ
	|	Документ.НаправлениеНаВнешнююАттестацию.Сотрудники КАК НаправлениеНаВнешнююАттестациюСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Сотрудник = ВТКадровыеДанныеСотрудников.Сотрудник
	|			И НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации = ВТКадровыеДанныеСотрудников.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеОтветственныхЛицОрганизации КАК ВТКадровыеДанныеРуководителей
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.Руководитель = ВТКадровыеДанныеРуководителей.ФизическоеЛицо
	|			И НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации = ВТКадровыеДанныеРуководителей.Период
	|ГДЕ
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка В(&ПриказыОбАттестации)
	|	И НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.ВидАттестации = &ВидАттестации";
	
	Результат = Запрос.Выполнить();

	Возврат Результат.Выбрать();
	
КонецФункции

#КонецОбласти

#Область ЗаявлениеОПодтвержденииКвалификации

Функция ТабличныйДокументЗаявлениеОПодтвержденииКвалификации(ПриказыОбАттестации, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб 			= Истина;
	ДокументРезультат.КлючПараметровПечати 	= "ПараметрыПечати_ЗаявлениеОПодтвержденииКвалификации";

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьВТСотрудникиПриказовОбАттестации(МенеджерВременныхТаблиц, ПриказыОбАттестации);

	ДанныеДляПечати = ДанныеДляПечатиЗаявлениеОПодтвержденииКвалификации(МенеджерВременныхТаблиц, ПриказыОбАттестации);
	
	СпециальностиФизическихЛиц = СпециальностиФизическихЛиц(МенеджерВременныхТаблиц);
	
	Пока ДанныеДляПечати.Следующий() Цикл
		
		Если ДокументРезультат.ВысотаТаблицы > 1 Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
		НомерПоПорядку 	= 1;
		
		КомплектДанных = КомплектДанныхДляПечатиЗаявленияОПодтвержденииКвалификации(ДанныеДляПечати, СпециальностиФизическихЛиц);
		ВывестиЗаявлениеОПодтвержденииКвалификации(ДокументРезультат, КомплектДанных);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НомерСтрокиНачало, ОбъектыПечати, ДанныеДляПечати.Ссылка);
		
	КонецЦикла;	
	
	Возврат ДокументРезультат;
	
КонецФункции

Процедура ВывестиЗаявлениеОПодтвержденииКвалификации(ДокументРезультат, КомплектДанных)
	
	ОбластиМакета = ОбластиМакетаЗаявлениеОПодтвержденииКвалификации();
	
	ДанныеДляПечати 				= КомплектДанных.ДанныеДляПечати;
	СпециальностиФизическогоЛица 	= КомплектДанных.СпециальностиФизическогоЛица;
	
	ДанныеЗаявления = Новый Структура;
	ДанныеЗаявления.Вставить("ФИОСотрудника", 						ДанныеДляПечати.ФИОСотрудника);
	ДанныеЗаявления.Вставить("Должность", 							ДанныеДляПечати.Должность);
	ДанныеЗаявления.Вставить("НаименованиеОрганизации", 			?(ЗначениеЗаполнено(ДанныеДляПечати.НаименованиеПолное), ДанныеДляПечати.НаименованиеПолное, ДанныеДляПечати.НаименованиеСокращенное));
	ДанныеЗаявления.Вставить("ТелефонМобильный", 					ДанныеДляПечати.ТелефонМобильныйПредставление);
	ДанныеЗаявления.Вставить("ПодтверждаемаяСпециальность", 		ДанныеДляПечати.ПодтверждаемаяСпециальность);
	ДанныеЗаявления.Вставить("ПодтверждаемаяКатегория", 			ДанныеДляПечати.ПодтверждаемаяКатегория);
	ДанныеЗаявления.Вставить("СтажРаботыПоСпециальности", 			"");
	ЗаполнитьЗначенияСвойств(ОбластиМакета.Шапка.Параметры, ДанныеЗаявления);
	ДокументРезультат.Вывести(ОбластиМакета.Шапка);
	
	ИмеющаясяКвалификационнаяКатегория = КвалификационнаяКатегорияПоСпециальности(СпециальностиФизическогоЛица, ДанныеДляПечати.ПодтверждаемаяСпециальность, ДанныеДляПечати.ДатаЗаседания);
	ДанныеЗаявления.Очистить();
	ДанныеЗаявления.Вставить("ПодтверждаемаяСпециальность", 		ДанныеДляПечати.ПодтверждаемаяСпециальность);
	Если ЗначениеЗаполнено(ИмеющаясяКвалификационнаяКатегория) Тогда
		ДанныеЗаявления.Вставить("ИмеющаясяКвалификационнаяКатегория", 	ИмеющаясяКвалификационнаяКатегория);
		ДанныеЗаявления.Вставить("ДатаПодтвержденияКатегории", 			Формат(ДатаПоследнегоПодтвержденияКвалификационнойКатегории(СпециальностиФизическогоЛица, ДанныеДляПечати.ПодтверждаемаяСпециальность, ДанныеДляПечати.ДатаЗаседания), "ДЛФ=D"));
		ЗаполнитьЗначенияСвойств(ОбластиМакета.КвалификационнаяКатегория.Параметры, ДанныеЗаявления);
		ДокументРезультат.Вывести(ОбластиМакета.КвалификационнаяКатегория);
	Иначе
		ЗаполнитьЗначенияСвойств(ОбластиМакета.КвалификационнаяКатегорияОтсутствует.Параметры, ДанныеЗаявления);
		ДокументРезультат.Вывести(ОбластиМакета.КвалификационнаяКатегорияОтсутствует);
	КонецЕсли;
	
	ДанныеЗаявления.Очистить();
	ДанныеЗаявления.Вставить("Дата", Формат(ДанныеДляПечати.Дата, "ДЛФ=D"));
	ЗаполнитьЗначенияСвойств(ОбластиМакета.Подвал.Параметры, ДанныеЗаявления);
	ДокументРезультат.Вывести(ОбластиМакета.Подвал);
	
КонецПроцедуры

Функция ОбластиМакетаЗаявлениеОПодтвержденииКвалификации()
	
	ОбластиМакета = Новый Структура;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьКадровыхПриказовМедицинскихОрганизаций.ПФ_MXL_ЗаявлениеОПодтвержденииКвалификации");
	ОбластиМакета.Вставить("Шапка", 								Макет.ПолучитьОбласть("Шапка"));
	ОбластиМакета.Вставить("КвалификационнаяКатегория", 			Макет.ПолучитьОбласть("КвалификационнаяКатегория"));
	ОбластиМакета.Вставить("КвалификационнаяКатегорияОтсутствует", 	Макет.ПолучитьОбласть("КвалификационнаяКатегорияОтсутствует"));
	ОбластиМакета.Вставить("Подвал", 								Макет.ПолучитьОбласть("Подвал"));
	
	Возврат ОбластиМакета;
	
КонецФункции

Функция ДанныеДляПечатиЗаявлениеОПодтвержденииКвалификации(МенеджерВременныхТаблиц, ПриказыОбАттестации)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПриказыОбАттестации", ПриказыОбАттестации);
	Запрос.УстановитьПараметр("ВидАттестации", Медицина.ВидАттестацииМедицинскихРаботников());
	НеобходимыеКадровыеДанные = "ФизическоеЛицо,ФИОПолные,Должность,ТелефонМобильныйПредставление";
	
	СоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, НеобходимыеКадровыеДанные);
	СоздатьВТКадровыеДанныеОтветственныхЛицОрганизации(Запрос.МенеджерВременныхТаблиц, ПриказыОбАттестации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриказОбАттестацииСотрудники.Ссылка КАК Ссылка,
	|	ПриказОбАттестацииСотрудники.Сотрудник КАК Сотрудник,
	|	ПриказОбАттестацииСотрудники.Специальность КАК ПодтверждаемаяСпециальность,
	|	ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания,
	|	Организации.НаименованиеПолное,
	|	Организации.НаименованиеСокращенное,
	|	ВТКадровыеДанныеРуководителей.ИОФамилия КАК ФИОРуководителя,
	|	ВТКадровыеДанныеСотрудников.ФизическоеЛицо,
	|	ВТКадровыеДанныеСотрудников.Должность,
	|	ВТКадровыеДанныеСотрудников.ТелефонМобильныйПредставление,
	|	ВТКадровыеДанныеСотрудников.ФИОПолные КАК ФИОСотрудника,
	|	ПриказОбАттестацииСотрудники.Категория КАК ПодтверждаемаяКатегория,
	|	ПриказОбАттестации.Дата,
	|	ПриказОбАттестации.ВидАттестации
	|ИЗ
	|	Документ.ПриказОбАттестацииСотрудников КАК ПриказОбАттестации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриказОбАттестацииСотрудников.Сотрудники КАК ПриказОбАттестацииСотрудники
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриказОбАттестацииСотрудников.ЗаседанияКомиссии КАК ПриказОбАттестацииЗаседанияКомиссии
	|			ПО ПриказОбАттестацииСотрудники.Ссылка = ПриказОбАттестацииЗаседанияКомиссии.Ссылка
	|				И ПриказОбАттестацииСотрудники.ИдентификаторСтрокиЗаседаниеКомиссии = ПриказОбАттестацииЗаседанияКомиссии.ИдентификаторСтрокиЗаседаниеКомиссии
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|			ПО ПриказОбАттестацииСотрудники.Сотрудник = ВТКадровыеДанныеСотрудников.Сотрудник
	|				И (ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания = ВТКадровыеДанныеСотрудников.Период)
	|		ПО (ПриказОбАттестацииСотрудники.Ссылка = ПриказОбАттестации.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ПриказОбАттестации.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеОтветственныхЛицОрганизации КАК ВТКадровыеДанныеРуководителей
	|		ПО ПриказОбАттестации.Руководитель = ВТКадровыеДанныеРуководителей.ФизическоеЛицо
	|			И (ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания = ВТКадровыеДанныеРуководителей.Период)
	|ГДЕ
	|	ПриказОбАттестации.Ссылка В(&ПриказыОбАттестации)
	|	И ПриказОбАттестации.ВидАттестации = &ВидАттестации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Сотрудник,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Специальность,
	|	НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации,
	|	Организации.НаименованиеПолное,
	|	Организации.НаименованиеСокращенное,
	|	ВТКадровыеДанныеРуководителей.ИОФамилия,
	|	ВТКадровыеДанныеСотрудников.ФизическоеЛицо,
	|	ВТКадровыеДанныеСотрудников.Должность,
	|	ВТКадровыеДанныеСотрудников.ТелефонМобильныйПредставление,
	|	ВТКадровыеДанныеСотрудников.ФИОПолные,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Категория,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.Дата,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.ВидАттестации
	|ИЗ
	|	Документ.НаправлениеНаВнешнююАттестацию.Сотрудники КАК НаправлениеНаВнешнююАттестациюСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Сотрудник = ВТКадровыеДанныеСотрудников.Сотрудник
	|			И НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации = ВТКадровыеДанныеСотрудников.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеОтветственныхЛицОрганизации КАК ВТКадровыеДанныеРуководителей
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.Руководитель = ВТКадровыеДанныеРуководителей.ФизическоеЛицо
	|			И НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации = ВТКадровыеДанныеРуководителей.Период
	|ГДЕ
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка В(&ПриказыОбАттестации)
	|	И НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.ВидАттестации = &ВидАттестации";
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выбрать();
	
КонецФункции

Функция КомплектДанныхДляПечатиЗаявленияОПодтвержденииКвалификации(ДанныеДляПечати, СпециальностиФизическихЛиц)
	
	КомплектДанных = Новый Структура;
	
	Отбор = Новый Структура("ФизическоеЛицо", ДанныеДляПечати.ФизическоеЛицо);
	
	КомплектДанных.Вставить("ДанныеДляПечати", 						ДанныеДляПечати);
	КомплектДанных.Вставить("СпециальностиФизическогоЛица", 		СпециальностиФизическихЛиц.НайтиСтроки(Отбор));
	
	Возврат КомплектДанных;
	
КонецФункции

#КонецОбласти

#Область АттестационныеЛистыМедицинскихРаботников

Функция АттестационныеЛистыМедицинскихРаботников(ПриказыОбАттестации, ОбъектыПечати) Экспорт
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб 			= Истина;
	ДокументРезультат.КлючПараметровПечати 	= "ПараметрыПечати_АттестационныйЛистМедицинскогоРаботника";

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьВТСотрудникиПриказовОбАттестации(МенеджерВременныхТаблиц, ПриказыОбАттестации);

	ДанныеДляПечати = ДанныеДляПечатиАттестационногоЛистаПодтверждениеКвалификации(МенеджерВременныхТаблиц, ПриказыОбАттестации);
	
	СоставыКомиссий = АттестацииСотрудников.СоставыАттестационныхКомиссийПриказовОбАттестации(ПриказыОбАттестации);
	ОбразованияФизическихЛиц = Обработки.ПечатьФормАттестацииСотрудников.ОбразованияФизическихЛицДляПечатиАттестационногоЛистаПодтверждениеКвалификации(МенеджерВременныхТаблиц);
	ТрудоваяДеятельностьФизическихЛиц = ТрудоваяДеятельностьФизическихЛицДляПечатиАттестационногоЛистаПодтверждениеКвалификации(МенеджерВременныхТаблиц);
	СпециальностиФизическихЛиц = СпециальностиФизическихЛиц(МенеджерВременныхТаблиц);
	
	Пока ДанныеДляПечати.Следующий() Цикл
		
		Если ДокументРезультат.ВысотаТаблицы > 1 Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
		НомерПоПорядку 	= 1;
		
		КомплектДанных = КомплектДанныхДляПечатиАттестационногоЛистаПодтверждениеКвалификации(ДанныеДляПечати, ОбразованияФизическихЛиц, ТрудоваяДеятельностьФизическихЛиц, СпециальностиФизическихЛиц, СоставыКомиссий);
		
		ВывестиАттестационныйЛистПодтверждениеКвалификации(ДокументРезультат, КомплектДанных);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НомерСтрокиНачало, ОбъектыПечати, ДанныеДляПечати.Ссылка);
		
	КонецЦикла;	
	
	Возврат ДокументРезультат;
	
КонецФункции

Процедура ВывестиАттестационныйЛистПодтверждениеКвалификации(ДокументРезультат, КомплектДанных)
	 		  
	ОбластиМакета = ОбластиМакетаАттестационныйЛистСотрудникаПодтверждениеКвалификации();

	ВывестиШапкуАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластиМакета.ОбластьШапка, КомплектДанных);
	
	ВывестиСведенияОбОбразованииАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластиМакета.ОбластьШапкаСведенийОбОбразовании, ОбластиМакета.ОбластьСтрокаОбразования, КомплектДанных);
	
	ВывестиСведенияОТрудовойДеятельностиАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластиМакета.ОбластьШапкаСведенийОТрудовойДеятельности, ОбластиМакета.ОбластьСтрокаТрудовойДеятельности, КомплектДанных);
	
	ВывестиПодвалАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластиМакета.ОбластьПодвал, КомплектДанных);
	
КонецПроцедуры

Процедура ВывестиШапкуАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластьШапка, КомплектДанных)
	
	ЗаполнитьЗначенияСвойств(ОбластьШапка.Параметры, КомплектДанных.ДанныеДляПечати);
	ДокументРезультат.Вывести(ОбластьШапка);
	
КонецПроцедуры

#Область СведенияОбОбразованииАттестационногоЛистаПодтверждениеКвалификации 

Процедура ВывестиСведенияОбОбразованииАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластьШапкаСведенийОбОбразовании, ОбластьСтрокаОбразования, КомплектДанных)
	
	ВывестиШапкуСведенийОбОбразованииАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластьШапкаСведенийОбОбразовании, КомплектДанных);
	
	ВывестиСтрокиСведенийОбОбразованииАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластьСтрокаОбразования, КомплектДанных);
	
КонецПроцедуры

Процедура ВывестиШапкуСведенийОбОбразованииАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластьШапкаСведенийОбОбразовании, КомплектДанных)
	ДанныеДляПечати = КомплектДанных.ДанныеДляПечати;
	ДанныеОбОбразовании = Новый Структура;
	ДанныеОбОбразовании.Вставить("УчебноеЗаведение", 					ДанныеДляПечати.ВысшееОбразованиеУчебноеЗаведение);
	ДанныеОбОбразовании.Вставить("ГодОкончанияУчебы", 					Формат(ДанныеДляПечати.ВысшееОбразованиеОкончание, "ДФ=yyyy"));
	ДанныеОбОбразовании.Вставить("СпециальностьПоОбразованию", 			ДанныеДляПечати.ВысшееОбразованиеСпециальность);
	ДанныеОбОбразовании.Вставить("РеквизитыДокументаОбОбразовании",  	ПредставлениеДокументаОбОсновномОбразованииДляАттестационногоЛистаПодтверждениеКвалификации(ДанныеДляПечати));
	ДанныеОбОбразовании.Вставить("ДатаВыдачиДокументаОбОбразовании", 	Формат(ДанныеДляПечати.ВысшееОбразованиеДатаВыдачи, "ДЛФ=DD"));
	ЗаполнитьЗначенияСвойств(ОбластьШапкаСведенийОбОбразовании.Параметры, ДанныеОбОбразовании);
	ДокументРезультат.Вывести(ОбластьШапкаСведенийОбОбразовании);
КонецПроцедуры

Процедура ВывестиСтрокиСведенийОбОбразованииАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластьСтрокаОбразования, КомплектДанных)
	ОбразованияФизическогоЛица = КомплектДанных.ОбразованияФизическогоЛица;
	Если ОбразованияФизическогоЛица.Количество() > 0 Тогда
		ДанныеОбОбразовании = Новый Структура;
		Для каждого ОбразованиеФизическогоЛица Из ОбразованияФизическогоЛица Цикл
			
			ДанныеОбОбразовании.Очистить();
			ДанныеОбОбразовании.Вставить("ВидОбразования", 			ПредставлениеВидаОбразованияДляАттестационногоЛистаПодтверждениеКвалификации(ОбразованиеФизическогоЛица));
			ДанныеОбОбразовании.Вставить("ПериодОбучения", 			ПредставлениеПериодаОбразованияДляАттестационногоЛистаПодтверждениеКвалификации(ОбразованиеФизическогоЛица));
			ДанныеОбОбразовании.Вставить("МестоОбучения", 			ОбразованиеФизическогоЛица.УчебноеЗаведение);
			ДанныеОбОбразовании.Вставить("НазваниеКурсаОбучения",  	ОбразованиеФизическогоЛица.НаименованиеКурса);
			
			ЗаполнитьЗначенияСвойств(ОбластьСтрокаОбразования.Параметры, ДанныеОбОбразовании);
			
			ДокументРезультат.Вывести(ОбластьСтрокаОбразования);
			
		КонецЦикла; 
	Иначе	
		ДокументРезультат.Вывести(ОбластьСтрокаОбразования);
	КонецЕсли;
КонецПроцедуры
	
Функция ПредставлениеДокументаОбОсновномОбразованииДляАттестационногоЛистаПодтверждениеКвалификации(ДанныеДляПечати)
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 %2 %3", ДанныеДляПечати.ВысшееОбразованиеВидДокумента, ДанныеДляПечати.ВысшееОбразованиеСерия, ДанныеДляПечати.ВысшееОбразованиеНомер);	
	
КонецФункции

Функция ПредставлениеВидаОбразованияДляАттестационногоЛистаПодтверждениеКвалификации(ОбразованиеФизическогоЛица)
	
	ПредставлениеВидаОбразования = "";
	
	Если ЗначениеЗаполнено(ОбразованиеФизическогоЛица.ВидОбразования) Тогда
		ПредставлениеВидаОбразования = ОбразованиеФизическогоЛица.ВидПослевузовскогоОбразования;
	ИначеЕсли ЗначениеЗаполнено(ОбразованиеФизическогоЛица.ВидДополнительногоОбучения) Тогда
		ПредставлениеВидаОбразования = ОбразованиеФизическогоЛица.ВидДополнительногоОбучения;
	Иначе
		ПредставлениеВидаОбразования = Нстр("ru = 'Дополнительное образование'");
	КонецЕсли;
	
	Возврат ПредставлениеВидаОбразования;
	
КонецФункции

Функция ПредставлениеПериодаОбразованияДляАттестационногоЛистаПодтверждениеКвалификации(ОбразованиеФизическогоЛица)
	
	ПредставлениеПериодаОбразования = "";
	
	Если Год(ОбразованиеФизическогоЛица.Начало) <> Год(ОбразованиеФизическогоЛица.Окончание) Тогда
		ПредставлениеПериодаОбразования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 - %2гг.", Формат(ОбразованиеФизическогоЛица.Начало, "ДФ=yyyy"),Формат(ОбразованиеФизическогоЛица.Окончание, "ДФ=yyyy"));
	ИначеЕсли ЗначениеЗаполнено(ОбразованиеФизическогоЛица.Начало) Тогда
		ПредставлениеПериодаОбразования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1г.", Формат(ОбразованиеФизическогоЛица.Начало, "ДФ=yyyy"));
	КонецЕсли;
	
	Возврат ПредставлениеПериодаОбразования;	
	
КонецФункции

#КонецОбласти

Процедура ВывестиСведенияОТрудовойДеятельностиАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластьШапкаСведенийОТрудовойДеятельности, ОбластьСтрокаТрудовойДеятельности, КомплектДанных)

	ДокументРезультат.Вывести(ОбластьШапкаСведенийОТрудовойДеятельности);
	
	ТрудоваяДеятельностьФизическогоЛица = КомплектДанных.ТрудоваяДеятельностьФизическогоЛица;
	
	Если ТрудоваяДеятельностьФизическогоЛица.Количество() > 0 Тогда
		ДанныеОТрудовойДеятельности = Новый Структура;
		Для каждого ПериодТрудовойДеятельности Из ТрудоваяДеятельностьФизическогоЛица Цикл
			
			ДанныеОТрудовойДеятельности.Очистить();
			ДанныеОТрудовойДеятельности.Вставить("ДатаНачалаТрудовойДеятельности", 			 ПериодТрудовойДеятельности.ДатаНачала);
			ДанныеОТрудовойДеятельности.Вставить("ДатаОкончанияТрудовойДеятельности", 		 ПериодТрудовойДеятельности.ДатаОкончания);
			ДанныеОТрудовойДеятельности.Вставить("ДолжностьОрганизацияТрудовойДеятельности", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1, %2", ПериодТрудовойДеятельности.Должность, ПериодТрудовойДеятельности.Организация));
			
			ЗаполнитьЗначенияСвойств(ОбластьСтрокаТрудовойДеятельности.Параметры, ДанныеОТрудовойДеятельности);
			
			ДокументРезультат.Вывести(ОбластьСтрокаТрудовойДеятельности);
			
		КонецЦикла; 
	Иначе	
		ДокументРезультат.Вывести(ОбластьСтрокаТрудовойДеятельности);
	КонецЕсли;

КонецПроцедуры

#Область ПодвалАттестационногоЛистаПодтверждениеКвалификации

Процедура ВывестиПодвалАттестационногоЛистаПодтверждениеКвалификации(ДокументРезультат, ОбластьПодвал, КомплектДанных)
	
	ДанныеДляПечати = КомплектДанных.ДанныеДляПечати;
	СоставКомиссии = КомплектДанных.СоставКомиссии;
	
	СпециальностиФизическогоЛица = КомплектДанных.СпециальностиФизическогоЛица;
	
	ДанныеПодвала = Новый Структура;
	ДанныеПодвала.Вставить("ФИОНачальникаОтделаКадров", 			ДанныеДляПечати.ФИОНачальникаОтделаКадров);
	ДанныеПодвала.Вставить("СтажРаботыВМедицинскихОрганизациях", 	АттестацииСотрудников.ПредставлениеСтажаСотрудникаДляАттестационногоЛиста(ДанныеДляПечати.ОбщийСтажЛет, ДанныеДляПечати.ОбщийСтажМесяцев));
	ДанныеПодвала.Вставить("ПодтверждаемаяКатегория", 				ДанныеДляПечати.ПодтверждаемаяКатегория);
	ДанныеПодвала.Вставить("ПодтверждаемаяСпециальность", 			ДанныеДляПечати.ПодтверждаемаяСпециальность);
	ДанныеПодвала.Вставить("СтажРаботыПоСпециальности", 			"");
	ДанныеПодвала.Вставить("ИмеющаясяКвалификационнаяКатегория", 	ПредставлениеКвалификационнаяКатегорияПоСпециальности(СпециальностиФизическогоЛица, ДанныеДляПечати.ПодтверждаемаяСпециальность, ДанныеДляПечати.ДатаЗаседания));    
	ДанныеПодвала.Вставить("ДругиеКвалификационныеКатегории", 		ПредставлениеДругиеКвалификационныеКатегории(СпециальностиФизическогоЛица, ДанныеДляПечати.ПодтверждаемаяСпециальность, ДанныеДляПечати.ДатаЗаседания));
	ДанныеПодвала.Вставить("УченыеСтепениИЗвания", 					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1, %2", ДанныеДляПечати.УченаяСтепень, ДанныеДляПечати.УченоеЗвание));
	ДанныеПодвала.Вставить("СведенияОНаучныхТрудах", 				"");
	ДанныеПодвала.Вставить("СведенияОбИзобретениях", 				"");
	ДанныеПодвала.Вставить("ЗнаниеЯзыков", 							ДанныеДляПечати.ЗнанияЯзыков);
	ДанныеПодвала.Вставить("АдресДляИнформирования", 				ДанныеДляПечати.АдресДляИнформированияПредставление);
	ДанныеПодвала.Вставить("ТелефонМобильный", 						ДанныеДляПечати.ТелефонМобильныйПредставление);
	ДанныеПодвала.Вставить("ЭлектроннаяПочта", 						"");
	ДанныеПодвала.Вставить("ДолжностьРуководителя", 				ДанныеДляПечати.ДолжностьРуководителя);
	ДанныеПодвала.Вставить("ФИОРуководителя", 						ДанныеДляПечати.ФИОРуководителя);
	ДанныеПодвала.Вставить("ФИОСекретаря", 							АттестацииСотрудников.ФИОЧленаКомиссииПоРоли(СоставКомиссии, Перечисления.РолиЧленовАттестационнойКомиссии.Секретарь, Истина));

	ЗаполнитьЗначенияСвойств(ОбластьПодвал.Параметры, ДанныеПодвала);
	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры

Функция ДатаПоследнегоПодтвержденияКвалификационнойКатегории(СпециальностиФизическогоЛица, ПодтверждаемаяСпециальность, ДатаЗаседания)
	
	 ДатаПоследнегоПодтверждения = Неопределено;
	
	Для каждого СпециальностьФизическогоЛица Из СпециальностиФизическогоЛица Цикл
		Если СпециальностьФизическогоЛица.Специальность = ПодтверждаемаяСпециальность 
			И КвалификационнаяКатегорияДействует(СпециальностьФизическогоЛица, ДатаЗаседания) Тогда
			ДатаПоследнегоПодтверждения = СпециальностьФизическогоЛица.ПериодЗаписи;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДатаПоследнегоПодтверждения; 	
	
КонецФункции

Функция КвалификационнаяКатегорияПоСпециальности(СпециальностиФизическогоЛица, ПодтверждаемаяСпециальность, ДатаЗаседания)
	
	ИмеющаясяКвалификационнаяКатегория = Неопределено;
	
	Для каждого СпециальностьФизическогоЛица Из СпециальностиФизическогоЛица Цикл
		Если СпециальностьФизическогоЛица.Специальность = ПодтверждаемаяСпециальность 
			И КвалификационнаяКатегорияДействует(СпециальностьФизическогоЛица, ДатаЗаседания) Тогда
			ИмеющаясяКвалификационнаяКатегория = СпециальностьФизическогоЛица.РазрядКатегория;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИмеющаясяКвалификационнаяКатегория; 	
	
КонецФункции

Функция ПредставлениеКвалификационнаяКатегорияПоСпециальности(СпециальностиФизическогоЛица, ПодтверждаемаяСпециальность, ДатаЗаседания)
	
	Представление = НСтр("ru = 'Не имеет'");
	
	ИмеющаясяКвалификационнаяКатегория = КвалификационнаяКатегорияПоСпециальности(СпециальностиФизическогоЛица, ПодтверждаемаяСпециальность, ДатаЗаседания);
	
	Если ЗначениеЗаполнено(ИмеющаясяКвалификационнаяКатегория) Тогда
	 Представление = ИмеющаясяКвалификационнаяКатегория;
	КонецЕсли;
	
	Возврат Представление; 	
	
КонецФункции

Функция ПредставлениеДругиеКвалификационныеКатегории(СпециальностиФизическогоЛица, ПодтверждаемаяСпециальность, ДатаЗаседания)
	
	ДругиеКвалификационныеКатегории = "";
	
	Для каждого СпециальностьФизическогоЛица Из СпециальностиФизическогоЛица Цикл
		Если СпециальностьФизическогоЛица.Специальность <> ПодтверждаемаяСпециальность
			И ЗначениеЗаполнено(СпециальностьФизическогоЛица.РазрядКатегория)
			И КвалификационнаяКатегорияДействует(СпециальностьФизическогоЛица, ДатаЗаседания) Тогда
			ДругиеКвалификационныеКатегории = ДругиеКвалификационныеКатегории 
											+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 (%2)", СпециальностьФизическогоЛица.Специальность,СпециальностьФизическогоЛица.РазрядКатегория) 
											+ Символы.ПС;
		КонецЕсли;
	КонецЦикла;
	
	СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(ДругиеКвалификационныеКатегории, 1);
	
	Если ПустаяСтрока(ДругиеКвалификационныеКатегории) Тогда
	  ДругиеКвалификационныеКатегории = НСтр("ru = 'Не имеет'");
	КонецЕсли;
	
	Возврат ДругиеКвалификационныеКатегории; 	
	
КонецФункции

Функция КвалификационнаяКатегорияДействует(СпециальностьФизическогоЛица, ДатаЗаседания)
	Возврат НЕ ЗначениеЗаполнено(СпециальностьФизическогоЛица.ДействуетПо) Или СпециальностьФизическогоЛица.ДействуетПо >= ДатаЗаседания;
КонецФункции
	
#КонецОбласти 

Функция ОбластиМакетаАттестационныйЛистСотрудникаПодтверждениеКвалификации()
	
	ОбластиМакета = Новый Структура;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьКадровыхПриказовМедицинскихОрганизаций.ПФ_MXL_АттестационныйЛистМедицинскогоРаботника");
	ОбластиМакета.Вставить("ОбластьШапка", 								Макет.ПолучитьОбласть("Шапка"));
	ОбластиМакета.Вставить("ОбластьШапкаСведенийОбОбразовании", 		Макет.ПолучитьОбласть("ШапкаСведенийОбОбразовании"));
	ОбластиМакета.Вставить("ОбластьСтрокаОбразования", 					Макет.ПолучитьОбласть("СтрокаОбразования"));
	ОбластиМакета.Вставить("ОбластьШапкаСведенийОТрудовойДеятельности", Макет.ПолучитьОбласть("ШапкаСведенийОТрудовойДеятельности"));
	ОбластиМакета.Вставить("ОбластьСтрокаТрудовойДеятельности", 		Макет.ПолучитьОбласть("СтрокаТрудовойДеятельности"));
	ОбластиМакета.Вставить("ОбластьПодвал", 							Макет.ПолучитьОбласть("Подвал"));
	
	Возврат ОбластиМакета;
	
КонецФункции

Функция КомплектДанныхДляПечатиАттестационногоЛистаПодтверждениеКвалификации(ДанныеДляПечати, ОбразованияФизическихЛиц, ТрудоваяДеятельностьФизическихЛиц, СпециальностиФизическихЛиц, СоставыКомиссий)
	
	КомплектДанных = Новый Структура;
	
	Отбор = Новый Структура("ФизическоеЛицо", ДанныеДляПечати.ФизическоеЛицо);
	
	КомплектДанных.Вставить("ДанныеДляПечати", 						ДанныеДляПечати);
	КомплектДанных.Вставить("ОбразованияФизическогоЛица", 			ОбразованияФизическихЛиц.НайтиСтроки(Отбор));
	КомплектДанных.Вставить("ТрудоваяДеятельностьФизическогоЛица", 	ТрудоваяДеятельностьФизическихЛиц.НайтиСтроки(Отбор));
	КомплектДанных.Вставить("СпециальностиФизическогоЛица", 		СпециальностиФизическихЛиц.НайтиСтроки(Отбор));
	КомплектДанных.Вставить("СоставКомиссии", 						СоставыКомиссий.Получить(ДанныеДляПечати.Ссылка));

	Возврат КомплектДанных;

КонецФункции

#Область ДанныеДляПечатиАттестационногоЛистаПодтверждениеКвалификации

Функция ДанныеДляПечатиАттестационногоЛистаПодтверждениеКвалификации(МенеджерВременныхТаблиц, ПриказыОбАттестации)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПриказыОбАттестации", ПриказыОбАттестации);
	
	НеобходимыеКадровыеДанные = ИменаКадровыхДанныхНеобходимыхДляПечатиАттестационногоЛистаПодтверждениеКвалификации();
	СоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, НеобходимыеКадровыеДанные);
	СоздатьВТКадровыеДанныеОтветственныхЛицОрганизации(Запрос.МенеджерВременныхТаблиц, ПриказыОбАттестации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриказОбАттестацииСотрудники.Ссылка КАК Ссылка,
	|	ПриказОбАттестацииСотрудники.Сотрудник КАК Сотрудник,
	|	ПриказОбАттестацииСотрудники.Подразделение,
	|	ПриказОбАттестацииСотрудники.Специальность КАК ПодтверждаемаяСпециальность,
	|	ПриказОбАттестацииСотрудники.Категория КАК ПодтверждаемаяКатегория,
	|	ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания,
	|	Организации.НаименованиеПолное,
	|	Организации.НаименованиеСокращенное,
	|	ВТКадровыеДанныеРуководителей.ИОФамилия КАК ФИОРуководителя,
	|	ПриказОбАттестации.ДолжностьРуководителя,
	|	ВТКадровыеДанныеНачальниковОтделаКадров.ИОФамилия КАК ФИОНачальникаОтделаКадров,
	|	ВТКадровыеДанныеСотрудников.ФИОПолные,
	|	ВТКадровыеДанныеСотрудников.ДатаРождения,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеУчебноеЗаведение,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеОкончание,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеВидДокумента,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеСерия,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеНомер,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеДатаВыдачи,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеСпециальность,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеКвалификация,
	|	ВТКадровыеДанныеСотрудников.УченаяСтепень,
	|	ВТКадровыеДанныеСотрудников.УченоеЗвание,
	|	ВТКадровыеДанныеСотрудников.ОбщийСтажЛет,
	|	ВТКадровыеДанныеСотрудников.ОбщийСтажМесяцев,
	|	ВТКадровыеДанныеСотрудников.ЗнанияЯзыков,
	|	ВТКадровыеДанныеСотрудников.ДатаПриема,
	|	ВТКадровыеДанныеСотрудников.АдресДляИнформированияПредставление,
	|	ВТКадровыеДанныеСотрудников.ТелефонМобильныйПредставление,
	|	ВТКадровыеДанныеСотрудников.ФизическоеЛицо
	|ИЗ
	|	Документ.ПриказОбАттестацииСотрудников КАК ПриказОбАттестации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриказОбАттестацииСотрудников.Сотрудники КАК ПриказОбАттестацииСотрудники
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриказОбАттестацииСотрудников.ЗаседанияКомиссии КАК ПриказОбАттестацииЗаседанияКомиссии
	|			ПО ПриказОбАттестацииСотрудники.Ссылка = ПриказОбАттестацииЗаседанияКомиссии.Ссылка
	|				И ПриказОбАттестацииСотрудники.ИдентификаторСтрокиЗаседаниеКомиссии = ПриказОбАттестацииЗаседанияКомиссии.ИдентификаторСтрокиЗаседаниеКомиссии
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|			ПО ПриказОбАттестацииСотрудники.Сотрудник = ВТКадровыеДанныеСотрудников.Сотрудник
	|				И (ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания = ВТКадровыеДанныеСотрудников.Период)
	|		ПО (ПриказОбАттестацииСотрудники.Ссылка = ПриказОбАттестации.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ПриказОбАттестации.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеОтветственныхЛицОрганизации КАК ВТКадровыеДанныеРуководителей
	|		ПО ПриказОбАттестации.Руководитель = ВТКадровыеДанныеРуководителей.ФизическоеЛицо
	|			И (ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания = ВТКадровыеДанныеРуководителей.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеОтветственныхЛицОрганизации КАК ВТКадровыеДанныеНачальниковОтделаКадров
	|		ПО ПриказОбАттестации.НачальникОтделаКадров = ВТКадровыеДанныеНачальниковОтделаКадров.ФизическоеЛицо
	|			И (ПриказОбАттестацииЗаседанияКомиссии.ДатаЗаседания = ВТКадровыеДанныеНачальниковОтделаКадров.Период)
	|ГДЕ
	|	ПриказОбАттестацииСотрудники.Ссылка В(&ПриказыОбАттестации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Сотрудник,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Подразделение,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Специальность,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Категория,
	|	НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации,
	|	Организации.НаименованиеПолное,
	|	Организации.НаименованиеСокращенное,
	|	ВТКадровыеДанныеРуководителей.ИОФамилия,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.ДолжностьРуководителя,
	|	ВТКадровыеДанныеНачальниковОтделаКадров.ИОФамилия,
	|	ВТКадровыеДанныеСотрудников.ФИОПолные,
	|	ВТКадровыеДанныеСотрудников.ДатаРождения,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеУчебноеЗаведение,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеОкончание,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеВидДокумента,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеСерия,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеНомер,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеДатаВыдачи,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеСпециальность,
	|	ВТКадровыеДанныеСотрудников.ВысшееОбразованиеКвалификация,
	|	ВТКадровыеДанныеСотрудников.УченаяСтепень,
	|	ВТКадровыеДанныеСотрудников.УченоеЗвание,
	|	ВТКадровыеДанныеСотрудников.ОбщийСтажЛет,
	|	ВТКадровыеДанныеСотрудников.ОбщийСтажМесяцев,
	|	ВТКадровыеДанныеСотрудников.ЗнанияЯзыков,
	|	ВТКадровыеДанныеСотрудников.ДатаПриема,
	|	ВТКадровыеДанныеСотрудников.АдресДляИнформированияПредставление,
	|	ВТКадровыеДанныеСотрудников.ТелефонМобильныйПредставление,
	|	ВТКадровыеДанныеСотрудников.ФизическоеЛицо
	|ИЗ
	|	Документ.НаправлениеНаВнешнююАттестацию.Сотрудники КАК НаправлениеНаВнешнююАттестациюСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Сотрудник = ВТКадровыеДанныеСотрудников.Сотрудник
	|			И НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации = ВТКадровыеДанныеСотрудников.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеОтветственныхЛицОрганизации КАК ВТКадровыеДанныеРуководителей
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.Руководитель = ВТКадровыеДанныеРуководителей.ФизическоеЛицо
	|			И НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации = ВТКадровыеДанныеРуководителей.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеОтветственныхЛицОрганизации КАК ВТКадровыеДанныеНачальниковОтделаКадров
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Ссылка.НачальникОтделаКадров = ВТКадровыеДанныеНачальниковОтделаКадров.ФизическоеЛицо
	|			И (НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации = ВТКадровыеДанныеРуководителей.Период)
	|ГДЕ
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка В(&ПриказыОбАттестации)";
	
	Результат = Запрос.Выполнить();

	Возврат Результат.Выбрать();
	
КонецФункции

Функция ИменаКадровыхДанныхНеобходимыхДляПечатиАттестационногоЛистаПодтверждениеКвалификации()
	
	КадровыеДанные = "ФизическоеЛицо,ФИОПолные,ДатаРождения,ЗнанияЯзыков,ДатаПриема,АдресДляИнформированияПредставление,ТелефонМобильныйПредставление";
	КадровыеДанные = КадровыеДанные + ",ВысшееОбразованиеУчебноеЗаведение,ВысшееОбразованиеОкончание,ВысшееОбразованиеСпециальность,ВысшееОбразованиеКвалификация,ВысшееОбразованиеВидДокумента,ВысшееОбразованиеСерия,ВысшееОбразованиеНомер,ВысшееОбразованиеДатаВыдачи,УченаяСтепень,УченоеЗвание";
	КадровыеДанные = КадровыеДанные + ",ОбщийСтажЛет,ОбщийСтажМесяцев";
	Возврат КадровыеДанные;

КонецФункции

Функция ТрудоваяДеятельностьФизическихЛицДляПечатиАттестационногоЛистаПодтверждениеКвалификации(МенеджерВременныхТаблиц)
	
	ОписаниеВТ = Новый Структура("ИмяВременнойТаблицыОтборовФизическихЛиц,ИмяПоляФизическоеЛицо", "ВТСотрудники", "ФизическоеЛицо");
	Запрос = КадровыйУчетРасширенный.ЗапросВТТрудоваяДеятельностьФизическихЛиц(Истина, ОписаниеВТ);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТТрудоваяДеятельностьФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТТрудоваяДеятельностьФизическихЛиц.ДатаНачала,
	|	ВТТрудоваяДеятельностьФизическихЛиц.ДатаОкончания,
	|	ВТТрудоваяДеятельностьФизическихЛиц.Организация,
	|	ВТТрудоваяДеятельностьФизическихЛиц.Должность
	|ИЗ
	|	ВТТрудоваяДеятельностьФизическихЛиц КАК ВТТрудоваяДеятельностьФизическихЛиц
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТТрудоваяДеятельностьФизическихЛиц.ФизическоеЛицо,
	|	ВТТрудоваяДеятельностьФизическихЛиц.ДатаНачала";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция СпециальностиФизическихЛиц(МенеджерВременныхТаблиц)
	
	ОписаниеВТ = Новый Структура("ИмяВременнойТаблицыОтборовФизическихЛиц,ИмяПоляПериод,ИмяПоляФизическоеЛицо", "ВТСотрудники", "Период", "ФизическоеЛицо");
	Запрос = КадровыйУчетРасширенный.ЗапросВТСпециальностиФизическихЛиц(Истина, ОписаниеВТ);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТСпециальностиФизическихЛиц.ПериодЗаписи КАК ПериодЗаписи,
	|	ВТСпециальностиФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТСпециальностиФизическихЛиц.Специальность,
	|	ВТСпециальностиФизическихЛиц.РазрядКатегория,
	|	ВТСпециальностиФизическихЛиц.ДействуетПо
	|ИЗ
	|	ВТСпециальностиФизическихЛиц КАК ВТСпециальностиФизическихЛиц
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТСпециальностиФизическихЛиц.ФизическоеЛицо";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции
	
#КонецОбласти

Процедура СоздатьВТСотрудникиПриказовОбАттестации(МенеджерВременныхТаблиц, ПриказыОбАттестации)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриказОбАттестацииСотрудниковСотрудники.Сотрудник,
	|	ПриказОбАттестацииСотрудниковСотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПриказОбАттестацииСотрудниковЗаседанияКомиссии.ДатаЗаседания КАК Период
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	Документ.ПриказОбАттестацииСотрудников.Сотрудники КАК ПриказОбАттестацииСотрудниковСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриказОбАттестацииСотрудников.ЗаседанияКомиссии КАК ПриказОбАттестацииСотрудниковЗаседанияКомиссии
	|		ПО ПриказОбАттестацииСотрудниковСотрудники.Ссылка = ПриказОбАттестацииСотрудниковЗаседанияКомиссии.Ссылка
	|			И ПриказОбАттестацииСотрудниковСотрудники.ИдентификаторСтрокиЗаседаниеКомиссии = ПриказОбАттестацииСотрудниковЗаседанияКомиссии.ИдентификаторСтрокиЗаседаниеКомиссии
	|ГДЕ
	|	ПриказОбАттестацииСотрудниковСотрудники.Ссылка В(&ПриказыОбАттестации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НаправлениеНаВнешнююАттестациюСотрудники.Сотрудник,
	|	НаправлениеНаВнешнююАттестациюСотрудники.Сотрудник.ФизическоеЛицо,
	|	НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации
	|ИЗ
	|	Документ.НаправлениеНаВнешнююАттестацию.Сотрудники КАК НаправлениеНаВнешнююАттестациюСотрудники
	|ГДЕ
	|	НаправлениеНаВнешнююАттестациюСотрудники.Ссылка В(&ПриказыОбАттестации)";
	
	Запрос.УстановитьПараметр("ПриказыОбАттестации", ПриказыОбАттестации);
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура СоздатьВТКадровыеДанныеСотрудников(МенеджерВременныхТаблиц, КадровыеДанные = "ФИОПолные")
	
	Описатель = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(МенеджерВременныхТаблиц, "ВТСотрудники");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(Описатель, Истина, КадровыеДанные);
	
КонецПроцедуры

Процедура СоздатьВТКадровыеДанныеОтветственныхЛицОрганизации(МенеджерВременныхТаблиц, ПриказыОбАттестации, КадровыеДанные = "ФИОПолные,ИОФамилия")
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриказОбАттестацииСотрудников.Руководитель КАК ФизическоеЛицо,
	|	ПриказОбАттестацииСотрудниковЗаседанияКомиссии.ДатаЗаседания КАК Период
	|ПОМЕСТИТЬ ВТФизическиеЛицаОбщийСписок
	|ИЗ
	|	Документ.ПриказОбАттестацииСотрудников.ЗаседанияКомиссии КАК ПриказОбАттестацииСотрудниковЗаседанияКомиссии
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриказОбАттестацииСотрудников КАК ПриказОбАттестацииСотрудников
	|		ПО ПриказОбАттестацииСотрудниковЗаседанияКомиссии.Ссылка = ПриказОбАттестацииСотрудников.Ссылка
	|ГДЕ
	|	ПриказОбАттестацииСотрудников.Ссылка В(&ПриказыОбАттестации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриказОбАттестацииСотрудников.НачальникОтделаКадров,
	|	ПриказОбАттестацииСотрудниковЗаседанияКомиссии.ДатаЗаседания
	|ИЗ
	|	Документ.ПриказОбАттестацииСотрудников.ЗаседанияКомиссии КАК ПриказОбАттестацииСотрудниковЗаседанияКомиссии
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриказОбАттестацииСотрудников КАК ПриказОбАттестацииСотрудников
	|		ПО ПриказОбАттестацииСотрудниковЗаседанияКомиссии.Ссылка = ПриказОбАттестацииСотрудников.Ссылка
	|ГДЕ
	|	ПриказОбАттестацииСотрудников.Ссылка В(&ПриказыОбАттестации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НаправлениеНаВнешнююАттестацию.Руководитель,
	|	НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации
	|ИЗ
	|	Документ.НаправлениеНаВнешнююАттестацию.Сотрудники КАК НаправлениеНаВнешнююАттестациюСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НаправлениеНаВнешнююАттестацию КАК НаправлениеНаВнешнююАттестацию
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Ссылка = НаправлениеНаВнешнююАттестацию.Ссылка
	|ГДЕ
	|	НаправлениеНаВнешнююАттестацию.Ссылка В(&ПриказыОбАттестации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НаправлениеНаВнешнююАттестацию.НачальникОтделаКадров,
	|	НаправлениеНаВнешнююАттестациюСотрудники.ДатаАттестации
	|ИЗ
	|	Документ.НаправлениеНаВнешнююАттестацию.Сотрудники КАК НаправлениеНаВнешнююАттестациюСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НаправлениеНаВнешнююАттестацию КАК НаправлениеНаВнешнююАттестацию
	|		ПО НаправлениеНаВнешнююАттестациюСотрудники.Ссылка = НаправлениеНаВнешнююАттестацию.Ссылка
	|ГДЕ
	|	НаправлениеНаВнешнююАттестацию.Ссылка В(&ПриказыОбАттестации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТФизическиеЛицаОбщийСписок.ФизическоеЛицо,
	|	ВТФизическиеЛицаОбщийСписок.Период
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	ВТФизическиеЛицаОбщийСписок КАК ВТФизическиеЛицаОбщийСписок
	|ГДЕ
	|	ВТФизическиеЛицаОбщийСписок.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТФизическиеЛицаОбщийСписок.Период,
	|	ВТФизическиеЛицаОбщийСписок.ФизическоеЛицо";
	Запрос.УстановитьПараметр("ПриказыОбАттестации", ПриказыОбАттестации);
	
	Запрос.Выполнить();
	
	Описатель = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеФизическихЛиц(Запрос.МенеджерВременныхТаблиц, "ВТФизическиеЛица");
	Описатель.ИмяВТКадровыеДанныеФизическихЛиц = "ВТКадровыеДанныеОтветственныхЛицОрганизации";
	КадровыйУчет.СоздатьВТКадровыеДанныеФизическихЛиц(Описатель, Истина, "ФИОПолные,ИОФамилия");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли