#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	
	ЗначенияДляЗаполнения = Новый Структура("Месяц, Организация", 
	"ДатаИндексации",
	"Организация");
			
	ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = ЗарплатаКадры.ПерваяДоступнаяОрганизация();
	КонецЕсли;
	
	ВидИндексацииГосударственныхСлужащих = Перечисления.ВидыИндексируемогоДенежногоСодержания.ОкладыДенежногоСодержания;
	ИндексацияВоеннослужащих = ГосударственнаяСлужбаВызовСервера.ТолькоВоеннаяСлужба();	
	КоэффициентИндексации 	= 1;
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "ДатаИндексации", "МесяцСтрокой");	
	
	УстановитьОтборСписка();
	ОбновитьИнформационныеНадписи();

КонецПроцедуры

&НаСервере
Функция СоздатьДокументыНаСервере(Регистраторы)
	
	ПараметрыИндексации = Обработки.ИндексацияОкладовГосударственнойСлужбы.СтруктураПараметровИндексации();
	ЗаполнитьЗначенияСвойств(ПараметрыИндексации, ЭтотОбъект);	
	ПараметрыИндексации.Дата 				= ТекущаяДата();
	ПараметрыИндексации.МесяцИндексации 	= ПараметрыИндексации.ДатаИндексации;
	ПараметрыИндексации.ДатаВступленияВСилу = ПараметрыИндексации.ДатаИндексации;
	ПараметрыИндексации.Регистраторы 		= Регистраторы;
		
	ДокументыИндексаторы = Обработки.ИндексацияОкладовГосударственнойСлужбы.СоздатьДокументыИндексации(ПараметрыИндексации);
	
	Возврат ДокументыИндексаторы;
	
КонецФункции

&НаКлиенте
Процедура СоздатьДокументыЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	ДокументыИндексаторы = СоздатьДокументыНаСервере(ДополнительныеПараметры.Регистраторы);

	ЭтаФорма.Элементы.ТаблицаДокументыИндексаций.Обновить();
	
	
	Для Каждого ДокументИндексатор Из ДокументыИндексаторы Цикл
		ОткрытьФормуОбъекта(ДокументИндексатор);
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)

	Если Не ЗначениеЗаполнено(ДатаИндексации) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не задана дата индексации.'"),
			,
			"ДатаИндексации");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не выбрана организация.'"),
			,
			"Организация");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидИндексацииГосударственныхСлужащих) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не задан вид индексации.'"),
			,
			"ВидИндексацииГосударственныхСлужащих");
		Возврат;
	КонецЕсли;
	
 	СуществующиеИндексаторы = ПолучитьИндексаторыНаДату(ДатаИндексации,Организация,ИндексацияВоеннослужащих, ВидИндексацииГосударственныхСлужащих);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Регистраторы", СуществующиеИндексаторы);
	
	Если СуществующиеИндексаторы.Количество()>0 Тогда
		
		ТекстВопроса = НСтр("ru = 'Документы индексации будут перезаписаны. Продолжить?'");
		Оповещение = Новый ОписаниеОповещения("СоздатьДокументыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		СоздатьДокументыЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);		
	КонецЕсли;
	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПриИзмененииМесяцаНачисления()
	УстановитьОтборСписка();
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияМесяцНачисленияПриИзменении()
	ПриИзмененииМесяцаНачисления();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	УстановитьОтборСписка();
КонецПроцедуры

&НаКлиенте
Процедура ИндексацияВоеннослужащихПриИзменении(Элемент)
	УстановитьОтборСписка();
	ОбновитьИнформационныеНадписи();
КонецПроцедуры


#Область РедактированиеМесяцаСтрокой

&НаКлиенте
Процедура МесяцСтрокойПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "ДатаИндексации", "МесяцСтрокой");
	ПриИзмененииМесяцаНачисления();	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("МесяцСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "ДатаИндексации", "МесяцСтрокой", , Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойНачалоВыбораЗавершение(ЗначениеВыбрано, ДополнительныеПараметры) Экспорт
	
	ПриИзмененииМесяцаНачисления();	
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "ДатаИндексации", "МесяцСтрокой", Направление);
	ПодключитьОбработчикОжидания("ОбработчикОжиданияМесяцНачисленияПриИзменении", 0.3, Истина);
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура ТаблицаДокументыИндексацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение( , Элемент.ТекущиеДанные.Документ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуОбъекта(СсылкаНаОбъект, ПараметрыОткрытия = Неопределено)
	
	Если ПараметрыОткрытия = Неопределено Тогда
		ПараметрыОткрытия = Новый Структура("Ключ", СсылкаНаОбъект);
	КонецЕсли; 
	
	ОткрытьФорму("Документ." + ИмяОбъектаМетаданных(СсылкаНаОбъект) + ".ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяОбъектаМетаданных(Ссылка)
	
	Возврат Ссылка.Метаданные().Имя;
	
КонецФункции

&НаСервере
Процедура ОбновитьИнформационныеНадписи()
	
	ЗаголовокФормы = "Индексация денежного содержания";
	Если ИндексацияВоеннослужащих Тогда
		ЗаголовокФормы = "Индексация денежного довольствия";
	КонецЕсли;
	
	ТолькоВоеннаяСлужба = ГосударственнаяСлужбаВызовСервера.ТолькоВоеннаяСлужба();
	Если ТолькоВоеннаяСлужба Тогда
		Элементы.ИндексацияВоеннослужащих.Видимость = Ложь;
	КонецЕсли;
	
	ЭтаФорма.Заголовок = ЗаголовокФормы;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСписка()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ДокументыИндексации, "Организация", Организация, ЗначениеЗаполнено(Организация));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ДокументыИндексации, "ДатаИндексации", ДатаИндексации, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ДокументыИндексации, "ИндексацияВоеннослужащих", ИндексацияВоеннослужащих, Истина);
		
	ОбновитьИнформационныеНадписи();
	                   
КонецПроцедуры

&НаСервере
Функция ПолучитьИндексаторыНаДату(ДатаИндексации,Организация,ИндексацияВоеннослужащих, ВидИндексацииГосударственныхСлужащих)
	
	СоответствиеТиповИндексаторов = Новый Соответствие();
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДатаИндексации",ДатаИндексации);
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ИндексацияВоеннослужащих",ИндексацияВоеннослужащих);
	Запрос.УстановитьПараметр("ИндексацияОкладовЗванийЧинов",ВидИндексацииГосударственныхСлужащих= Перечисления.ВидыИндексируемогоДенежногоСодержания.ОкладыДенежногоСодержания 
		ИЛИ ВидИндексацииГосударственныхСлужащих= Перечисления.ВидыИндексируемогоДенежногоСодержания.ОкладыПоЗванию);
	
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДокументИндексации.Ссылка КАК Документ
	               |ИЗ
	               |	Документ.ИндексацияШтатногоРасписания КАК ДокументИндексации
	               |ГДЕ
	               |	ДокументИндексации.Организация = &Организация
	               |	И ДокументИндексации.МесяцИндексации = &ДатаИндексации
	               |	И ДокументИндексации.ИндексацияВоеннослужащих = &ИндексацияВоеннослужащих
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ДокументИндексации.Ссылка
	               |ИЗ
	               |	Документ.ИндексацияЗаработка КАК ДокументИндексации
	               |ГДЕ
	               |	ДокументИндексации.Организация = &Организация
	               |	И ДокументИндексации.МесяцИндексации = &ДатаИндексации
	               |	И ДокументИндексации.ИндексацияВоеннослужащих = &ИндексацияВоеннослужащих
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ДокументИндексации.Ссылка
	               |ИЗ
	               |	Документ.УтверждениеОкладовЗаВоинскиеСпециальныеЗвания КАК ДокументИндексации
	               |ГДЕ
	               |	&ИндексацияВоеннослужащих
	               |	И &ИндексацияОкладовЗванийЧинов
	               |	И ДокументИндексации.ДатаВступленияВСилу = &ДатаИндексации
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ДокументИндексации.Ссылка
	               |ИЗ
	               |	Документ.УтверждениеОкладовНадбавокЗаКлассныеЧины КАК ДокументИндексации
	               |ГДЕ
	               |	НЕ &ИндексацияВоеннослужащих
	               |	И &ИндексацияОкладовЗванийЧинов
	               |	И ДокументИндексации.ДатаВступленияВСилу = &ДатаИндексации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СоответствиеТиповИндексаторов.Вставить(Выборка.Документ.Метаданные().Имя,Выборка.Документ);
	КонецЦикла;
	Возврат СоответствиеТиповИндексаторов;
	
КонецФункции

#КонецОбласти

