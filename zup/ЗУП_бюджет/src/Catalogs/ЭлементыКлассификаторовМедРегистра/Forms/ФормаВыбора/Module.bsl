
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВладелецЭлементов = Неопределено;
	Если Параметры.Свойство("Владелец", ВладелецЭлементов) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЭтаФорма.ЭлементыКлассификатора, "Владелец", ВладелецЭлементов);
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Владелец", ВладелецЭлементов) Тогда
		Если ТипЗнч(ВладелецЭлементов) = Тип("СправочникСсылка.КлассификаторыМедРегистра") И ЗначениеЗаполнено(ВладелецЭлементов) Тогда
			Заголовок = ВладелецЭлементов.ПолноеНаименование();
			АвтоЗаголовок = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВладелецЭлементов) 
		И Не Справочники.КлассификаторыМедРегистра.КлассификаторИспользуетВыборГруппИЭлементов(ВладелецЭлементов) Тогда  
		Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы;
	КонецЕсли;

	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)	
	СтандартнаяОбработка = Не ЭтоОтборСпециальностиПоСертификатуКорневогоУровня();
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ЭтоОтборСпециальностиПоСертификатуКорневогоУровня()
	
	ДанныеСтроки = Элементы.Список.ТекущиеДанные;
	
	Если ДанныеСтроки <> Неопределено Тогда	
		
		Если ДанныеСтроки.ЭтоГруппа И Не ЗначениеЗаполнено(ДанныеСтроки.Родитель)
			И ВладелецЭлементов = ПредопределенноеЗначение("Справочник.КлассификаторыМедРегистра.ArrayOfCertificateSpeciality") Тогда 			
			Возврат Истина;			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

#КонецОбласти

