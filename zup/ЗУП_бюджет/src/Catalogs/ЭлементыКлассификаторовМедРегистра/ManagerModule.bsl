#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает реквизиты справочника, которые образуют естественный ключ
//  для элементов справочника.
// Используется для сопоставления элементов механизмом «Выгрузка/загрузка областей данных».
//
// Возвращаемое значение: Массив(Строка) - массив имен реквизитов, образующих
//  естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Владелец");
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает структуру гражданства в мед. регистре.
//
Функция ГражданствоМедицинскихРаботников() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ЭлементыКлассификаторовМедРегистра.Ссылка,
	|	ВЫБОР
	|		КОГДА ЭлементыКлассификаторовМедРегистра.Код = ""1""
	|			ТОГДА ""ГражданинРоссии""
	|		КОГДА ЭлементыКлассификаторовМедРегистра.Код = ""3""
	|			ТОГДА ""ИностранныйГражданин""
	|		КОГДА ЭлементыКлассификаторовМедРегистра.Код = ""4""
	|			ТОГДА ""БезГражданства""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Код
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец", Справочники.КлассификаторыМедРегистра.ArrayOfCitizenship);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Гражданство = Новый Структура("ГражданинРоссии, ИностранныйГражданин, БезГражданства");
	
	Пока Выборка.Следующий() Цикл 
		
		Если ПустаяСтрока(Выборка.Код ) Тогда
			Продолжить;
		КонецЕсли;
		
		Гражданство.Вставить(Выборка.Код, Выборка.Ссылка);
		
	КонецЦикла;
	
	Возврат Гражданство;
	
КонецФункции	

Функция КлючевыеХарактеристикиКадровойИстории() Экспорт

	КлючевыеХарактеристики = Новый Структура;
	КлючевыеХарактеристики.Вставить("Прием");
	КлючевыеХарактеристики.Вставить("НачалоВнутреннееПеремещение");
	КлючевыеХарактеристики.Вставить("ПриемПослеОбучения");
	КлючевыеХарактеристики.Вставить("ПриемИзДругогоСубъектаРФ");
	КлючевыеХарактеристики.Вставить("ПриемИзДругойСтраны");
	КлючевыеХарактеристики.Вставить("ОкончаниеВнутреннееПеремещение");
	КлючевыеХарактеристики.Вставить("УволенПоУсловиямДоговора");
	КлючевыеХарактеристики.Вставить("УволенПоСобственномуЖеланию");
	КлючевыеХарактеристики.Вставить("ВыбылВДругуюСтрану");
	КлючевыеХарактеристики.Вставить("ВыбылВДругойСубъектРФ");
	КлючевыеХарактеристики.Вставить("ОкончаниеТрудовогоДоговора");
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	""Прием"" КАК Ключ,
	|	ЭлементыКлассификаторовМедРегистра.Ссылка КАК Значение
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""9""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeIn)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""НачалоВнутреннееПеремещение"",
	|	ЭлементыКлассификаторовМедРегистра.Ссылка
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""8""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeIn)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПриемПослеОбучения"",
	|	ЭлементыКлассификаторовМедРегистра.Ссылка
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""2""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeInAdd)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПриемИзДругогоСубъектаРФ"",
	|	ЭлементыКлассификаторовМедРегистра.Ссылка
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""3""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeInAdd)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПриемИзДругойСтраны"",
	|	ЭлементыКлассификаторовМедРегистра.Ссылка
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""4""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeInAdd)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ОкончаниеВнутреннееПеремещение"",
	|	ЭлементыКлассификаторовМедРегистра.Ссылка
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""12""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeOut)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""УволенПоУсловиямДоговора"",
	|	ЭлементыКлассификаторовМедРегистра.Ссылка
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""2""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeOut)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""УволенПоСобственномуЖеланию"",
	|	ЭлементыКлассификаторовМедРегистра.Ссылка
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""3""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeOut)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ОкончаниеТрудовогоДоговора"",
	|	ЭлементыКлассификаторовМедРегистра.Ссылка
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""4""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeOut)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ВыбылВДругуюСтрану"",
	|	ЭлементыКлассификаторовМедРегистра.Ссылка
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""4""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeOutDel)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ВыбылВДругойСубъектРФ"",
	|	ЭлементыКлассификаторовМедРегистра.Ссылка
	|ИЗ
	|	Справочник.ЭлементыКлассификаторовМедРегистра КАК ЭлементыКлассификаторовМедРегистра
	|ГДЕ
	|	ЭлементыКлассификаторовМедРегистра.Код = ""3""
	|	И ЭлементыКлассификаторовМедРегистра.Владелец = ЗНАЧЕНИЕ(Справочник.КлассификаторыМедРегистра.ArrayOfRecordTypeOutDel)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КлючевыеХарактеристики.Вставить(Выборка.Ключ, Выборка.Значение);
	КонецЦикла;
	
	Возврат КлючевыеХарактеристики;
	
КонецФункции

#КонецОбласти

#КонецЕсли

