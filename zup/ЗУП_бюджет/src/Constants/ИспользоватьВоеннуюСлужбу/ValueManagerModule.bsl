#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий	
	
Процедура ПередЗаписью(Отказ)
	
	ПодсистемаСуществует = ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба");
	Если Не ПодсистемаСуществует Тогда
		Если Значение Тогда
			ВызватьИсключение НСтр("ru = 'Нельзя установить значение ИспользоватьВоеннуюСлужбу'");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПодсистемаСуществует Тогда
		ИспользоватьМуниципальнуюСлужбу = Константы.ИспользоватьМуниципальнуюСлужбу.Получить();
		Если Значение И ИспользоватьМуниципальнуюСлужбу Тогда
			ВызватьИсключение НСтр("ru = 'В программе уже ведется расчет денежного содержания Муниципальных служащих, не допускается использовать одновременно расчет денежного довольствия военнослужащих и денежного содержания муниципальных служащих'");	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Подключаемые Характеристики
	ИсточникиХарактеристик = Новый Массив;
	ИсточникиХарактеристик.Добавить("СвойстваДолжностейГосударственнойСлужбы");
	ИсточникиХарактеристик.Добавить("СвойстваНачисленийГосударственныхСлужащих");
	ИсточникиХарактеристик.Добавить("СтрокиОтчетностиРасходовИЧисленностиРаботниковГосударственныхОрганов");
	ПодключаемыеХарактеристикиЗарплатаКадры.ОбновитьНаборыПодключаемыхХарактеристик(ЭтотОбъект.Значение, ИсточникиХарактеристик);
	
	ПодсистемаСуществует = ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба");
	Если ПодсистемаСуществует И ПолучитьФункциональнуюОпцию("ИспользоватьВоеннуюСлужбу") И Значение Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Модуль.ЗаполнитьДополнительныеСвойстваНачисленийРКиСН();
	КонецЕсли;
	
	ГосударственнаяСлужба.УстановитьПараметрыНабораСвойствВоинскиеСпециальныеЗвания();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли