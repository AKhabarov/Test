#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует табличный документ отчет.
//
// Параметры:
//  ТаблицаОтчета 	- ТабличныйДокумент - документ, в который выводятся данные.
//  Организация  	- СправочникСсылка.Организации - Организация, по которой формируется отчет.
//  ДатаНачала		- Дата - дата начала периода.
//  ДатаОкончания	- Дата - дата конца периода.
//
Процедура СформироватьОтчет(ТаблицаОтчета,Организация, ДатаНачала, ДатаОкончания) Экспорт
	
	ТаблицаОтчета.Очистить();
			
	СформироватьОтчетФорма14МО(ТаблицаОтчета, Организация, ДатаНачала, ДатаОкончания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиРаздел(ТабДокРезультат, ВыборкаСтрок, ТекущийРаздел, ТочностьПоказателей, Макет) Экспорт
	
	ТочностьВ10 = Pow(10, ТочностьПоказателей);
	БоковикПоказатели = Макет.ПолучитьОбласть("Раздел_"+ТекущийРаздел+"|Боковик");
	ОбластьПоказатели = Макет.ПолучитьОбласть("Раздел_"+ТекущийРаздел+"|Всего");
	БоковикШапка	  = Макет.ПолучитьОбласть("Св_"+ТекущийРаздел+"|Бюдж");
	ШапкаАналитики	  = Макет.ПолучитьОбласть("Св_"+ТекущийРаздел+"|ПоРазделам");
	ШапкаВсего		  = Макет.ПолучитьОбласть("Св_"+ТекущийРаздел+"|ВСего");
	Шапка		  = Макет.ПолучитьОбласть("Шапка_"+ТекущийРаздел);
	
	ТабДокРезультат.Вывести(Шапка);
	
	ТабДокументШапка      = Новый ТабличныйДокумент;
	ДетальныеПоказатели   = Новый ТабличныйДокумент;
	
	ТабДокументШапка.Вывести(БоковикШапка);
	
	ИтогоСтруктураПараметров = ПолучитьНазваниеПараметровОбласти(ОбластьПоказатели);
	СтруктураПараметровЗнач = ПолучитьНазваниеПараметровОбласти(ОбластьПоказатели);

	Пока ВыборкаСтрок.Следующий() Цикл //Цикл по разделам отчета
		
		ЗаполнитьЗначенияСвойств(СтруктураПараметровЗнач, ВыборкаСтрок);
		Для каждого Параметр Из ИтогоСтруктураПараметров Цикл
			СтруктураПараметровЗнач[Параметр.Ключ]  = ?(СтруктураПараметровЗнач[Параметр.Ключ] = NULL, 0, СтруктураПараметровЗнач[Параметр.Ключ])/ТочностьВ10;
			ИтогоСтруктураПараметров[Параметр.Ключ] = ИтогоСтруктураПараметров[Параметр.Ключ] + СтруктураПараметровЗнач[Параметр.Ключ];
		КонецЦикла;
		ШапкаАналитики.Параметры.Заполнить(ВыборкаСтрок);
		ТабДокументШапка.Присоединить(ШапкаАналитики);
		
	КонецЦикла;	
		
	ТабДокРезультат.Вывести(ТабДокументШапка);
	ТабДокРезультат.Вывести(БоковикПоказатели);
	ОбластьПоказатели.Параметры.Заполнить(ИтогоСтруктураПараметров);
	ТабДокРезультат.Присоединить(ОбластьПоказатели);
	
	ВыборкаСтрок.Сбросить();
	Пока ВыборкаСтрок.Следующий() Цикл //Цикл по разделам отчета
		
		ЗаполнитьЗначенияСвойств(СтруктураПараметровЗнач, ВыборкаСтрок);
		Для каждого Параметр Из ИтогоСтруктураПараметров Цикл
			СтруктураПараметровЗнач[Параметр.Ключ]  = СтруктураПараметровЗнач[Параметр.Ключ]/ТочностьВ10;
		КонецЦикла;
		ОбластьПоказатели.Параметры.Заполнить(СтруктураПараметровЗнач);
		ТабДокРезультат.Присоединить(ОбластьПоказатели);
		
	КонецЦикла;	
	
КонецПроцедуры

Функция ПолучитьНазваниеПараметровОбласти(ОбластьПоказатели)
	СтруктураПоказателей = Новый Структура;
	Для НомерСтроки = 1 По ОбластьПоказатели.ВысотаТаблицы Цикл
		Для НомерКолонки = 1 По ОбластьПоказатели.ШиринаТаблицы Цикл
			Ячейка = ОбластьПоказатели.Область(НомерСтроки,НомерКолонки,НомерСтроки,НомерКолонки);
			Если Ячейка.Параметр <> "" И Ячейка.Параметр <> Неопределено Тогда
				Если НЕ СтруктураПоказателей.Свойство(Ячейка.Параметр) Тогда
					СтруктураПоказателей.Вставить(Ячейка.Параметр, 0);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Возврат СтруктураПоказателей;
КонецФункции

Функция ДатаПереходаНа179н() Экспорт
	Возврат КонецГода('20101231235959')
КонецФункции

Функция ДатаПереходаНа143н() Экспорт
	Возврат КонецГода('20141231235959')
КонецФункции

Функция ДатаПереходаНа86н() Экспорт
	Возврат Дата(2016,1,1)
КонецФункции

Функция ДатаПереходаНа259н() Экспорт
	Возврат Дата(2017,1,1)
КонецФункции

Функция ПолучитьДанныеПоФорме14МОРасходы(Организация, ДатаНачала, ДатаОкончания) экспорт
	
	СтруктураДанных = Новый Структура("Расходы, Справка");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудников.Организация 		= Организация;
	ПараметрыПолученияСотрудников.НачалоПериода 	= ДатаНачала;
	ПараметрыПолученияСотрудников.ОкончаниеПериода	= ДатаОкончания;
			
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(
			Запрос.МенеджерВременныхТаблиц, Истина, 
			ПараметрыПолученияСотрудников, 
			"ВТСотрудникиОрганизации");
			
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиОрганизации");
	КадровыеДанные = "ВидДоговора";
	ЗарплатаКадры.ДополнитьКадровымиДаннымиНастройкиПорядкаСписка(КадровыеДанные);
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, КадровыеДанные);			
		
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
		
	// начисления, которые показываются в отчетах в составе других начислений
	МассивВРДляРаспределения = Новый Массив;
	МассивВРДляРаспределения.Добавить(РасчетЗарплаты.НачислениеРайонныйКоэффициент());
	МассивВРДляРаспределения.Добавить(РасчетЗарплаты.НачислениеСевернаяНадбавка());
	Запрос.УстановитьПараметр("МассивВРДляРаспределения", МассивВРДляРаспределения);
	
	МассивВидыДоговоров = Новый Массив;
	МассивВидыДоговоров.Добавить(Перечисления.ВидыДоговоровССотрудниками.ДоговорМуниципальногоСлужащего);
	Запрос.УстановитьПараметр("МассивВидыДоговоров", МассивВидыДоговоров);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("парамИзмерения", Измерения);
	
	Разрезы = Новый Массив;
	Разрезы.Добавить("Регистратор");
	Разрезы.Добавить("ВидРасчета");
	Разрезы.Добавить("ПериодРегистрации");
	Запрос.УстановитьПараметр("парамРазрезы", Разрезы);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияБазаНачисления.Сотрудник КАК Сотрудник,
	|	НачисленияБазаНачисления.ПериодРегистрацииРазрез КАК Период,
	|	НачисленияБазаНачисления.РегистраторРазрез КАК Регистратор,
	|	НачисленияБазаНачисления.ВидРасчетаРазрез КАК ВидРасчета,
	|	НачисленияБазаНачисления.РезультатБаза КАК Результат,
	|	НачисленияБазаНачисления.ПериодРегистрации КАК ПериодВР,
	|	НачисленияБазаНачисления.Регистратор КАК РегистраторВР,
	|	НачисленияБазаНачисления.ВидРасчета КАК ВидРасчетаВР,
	|	НачисленияБазаНачисления.Результат КАК РезультатВР,
	|	НачисленияБазаНачисления.Сторно КАК Сторно
	|ПОМЕСТИТЬ ВТВРДляРаспределения
	|ИЗ
	|	РегистрРасчета.Начисления.БазаНачисления(
	|			&ПарамИзмерения,
	|			&ПарамИзмерения,
	|			&ПарамРазрезы,
	|			ВидРасчета В (&МассивВРДляРаспределения)
	|				И Организация = &Организация
	|				И (ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода)
	|				И Результат <> 0) КАК НачисленияБазаНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ПО НачисленияБазаНачисления.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
	|ГДЕ
	|	КадровыеДанныеСотрудников.ВидДоговора В (&МассивВидыДоговоров)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПромежуточныеДанные.РегистраторВР
	|ПОМЕСТИТЬ ВТСторнировавшиеДокументы
	|ИЗ
	|	ВТВРДляРаспределения КАК ПромежуточныеДанные
	|ГДЕ
	|	ПромежуточныеДанные.Сторно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПромежуточныеДанные.Сотрудник,
	|	ПромежуточныеДанные.Период,
	|	ПромежуточныеДанные.ВидРасчета,
	|	ВЫБОР
	|		КОГДА ПромежуточныеДанные.РегистраторВР В
	|				(ВЫБРАТЬ
	|					СторнировавшиеДокументы.РегистраторВР
	|				ИЗ
	|					ВТСторнировавшиеДокументы КАК СторнировавшиеДокументы)
	|			ТОГДА ПромежуточныеДанные.РегистраторВР
	|		ИНАЧЕ ПромежуточныеДанные.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	ПромежуточныеДанные.Результат,
	|	ПромежуточныеДанные.ПериодВР,
	|	ПромежуточныеДанные.РегистраторВР,
	|	ПромежуточныеДанные.ВидРасчетаВР,
	|	ПромежуточныеДанные.РезультатВР
	|ПОМЕСТИТЬ ВТБаза
	|ИЗ
	|	ВТВРДляРаспределения КАК ПромежуточныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияВР.Регистратор КАК РегистраторВР,
	|	НачисленияВР.НомерСтроки КАК НомерСтрокиВР,
	|	НачисленияВР.Сумма КАК РезультатВР,
	|	Начисления.Регистратор,
	|	Начисления.НомерСтроки,
	|	Начисления.Сумма КАК Результат
	|ИЗ
	|	ВТБаза КАК База
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияВР
	|		ПО (НачисленияВР.Регистратор = База.РегистраторВР)
	|			И (НачисленияВР.Период = База.ПериодВР)
	|			И (НачисленияВР.Сотрудник = База.Сотрудник)
	|			И (НачисленияВР.НачислениеУдержание = База.ВидРасчетаВР)
	|			И (НачисленияВР.Организация = &Организация)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК Начисления
	|		ПО (Начисления.Регистратор = База.Регистратор)
	|			И (Начисления.Период = База.Период)
	|			И (Начисления.Сотрудник = База.Сотрудник)
	|			И (Начисления.НачислениеУдержание = База.ВидРасчета)
	|			И (Начисления.Организация = &Организация)	
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачисленияВР.Регистратор,
	|	НачисленияВР.НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаРегистраторов = РегистрыРасчета.Начисления.СоздатьНаборЗаписей().ВыгрузитьКолонки("Регистратор");
	МассивТиповДокументов = Новый Массив;
	Для каждого ТипДокумента Из ТаблицаРегистраторов.Колонки.Регистратор.ТипЗначения.Типы() Цикл
		МассивТиповДокументов.Добавить(ТипДокумента);
	КонецЦикла;
	
	ТаблицаРаспределения = Новый ТаблицаЗначений;
	ТаблицаРаспределения.Колонки.Добавить("Регистратор", Новый ОписаниеТипов(МассивТиповДокументов));
	ТаблицаРаспределения.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаРаспределения.Колонки.Добавить("Результат", Новый ОписаниеТипов("Число"));
	
	ВремТаблица = ТаблицаРаспределения.СкопироватьКолонки();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("РегистраторВР") Цикл
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтрокиВР") Цикл
			
			СуммаКРаспределению = Выборка.РезультатВР;
			
			ВремТаблица.Очистить();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ВремТаблица.Добавить(),Выборка);
			КонецЦикла;
			
			МассивКоэффициентов = ВремТаблица.ВыгрузитьКолонку("Результат");
			МассивСуммКНачислению = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СуммаКРаспределению,МассивКоэффициентов);
			
			Если МассивСуммКНачислению <> Неопределено Тогда
				
				Для каждого СтрокаТЗ Из ВремТаблица Цикл
					
					НоваяСтрока = ТаблицаРаспределения.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТЗ);
					НоваяСтрока.Результат = МассивСуммКНачислению[ВремТаблица.Индекс(СтрокаТЗ)];
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	ТаблицаРаспределения.Свернуть("Регистратор,НомерСтроки","Результат");
	Запрос.УстановитьПараметр("ТаблицаРаспределения", ТаблицаРаспределения);
	
	МуниципальныеДолжности = Новый Массив;
	МуниципальныеДолжности.Добавить("МуниципальнаяДолжность");
	МуниципальныеДолжности.Добавить("ДолжностьМуниципальнойСлужбы");
	Запрос.УстановитьПараметр("МуниципальныеДолжности", МуниципальныеДолжности);
	
	МассивКОСГУ = Новый Массив;
	МассивКОСГУ.Добавить(ОтражениеЗарплатыВБухучетеРасширенный.СтатьяРасходов211());
	Запрос.УстановитьПараметр("КОСГУ", МассивКОСГУ);
	
	ПараметрыПолученияРабочихМест = КадровыйУчет.ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоСпискуСотрудников();
	ЗаполнитьЗначенияСвойств(ПараметрыПолученияРабочихМест,ПараметрыПолученияСотрудников);
	// Получаем рабочие места сотрудников
	КадровыйУчет.СоздатьВТРабочиеМестаСотрудников(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияРабочихМест);
			
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаРаспределения.Регистратор КАК Регистратор,
	|	ТаблицаРаспределения.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРаспределения.Результат
	|ПОМЕСТИТЬ ВТТаблицаРаспределения
	|ИЗ
	|	&ТаблицаРаспределения КАК ТаблицаРаспределения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаботникиОрганизации.Период КАК Период,
	|	РаботникиОрганизации.Сотрудник КАК Сотрудник,
	|	РаботникиОрганизации.Должность,
	|	ЕСТЬNULL(СтрокиОтчетностиРасходовИЧисленности.СтрокаОтчетностиРасходовИЧисленностиРаботниковГосударственныхОрганов.КодКатегорииПерсонала,"""") КАК КодКатегорииПерсонала,
	|	СвойстваДолжностейГосударственнойСлужбы.ГруппаДолжностиГосударственнойСлужбы
	|ПОМЕСТИТЬ ВТДвиженияРаботников
	|ИЗ
	|	ВТРабочиеМестаСотрудников КАК РаботникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтрокиОтчетностиРасходовИЧисленностиРаботниковГосударственныхОрганов КАК СтрокиОтчетностиРасходовИЧисленности
	|		ПО (СтрокиОтчетностиРасходовИЧисленности.Должность = РаботникиОрганизации.Должность)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваДолжностейГосударственнойСлужбы КАК СвойстваДолжностейГосударственнойСлужбы
	|		ПО (СвойстваДолжностейГосударственнойСлужбы.Должность = РаботникиОрганизации.Должность)
	|ГДЕ
	|	РаботникиОрганизации.Организация = &Организация
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(Начисления.Период, МЕСЯЦ) КАК Период,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.НачислениеУдержание КАК ВидРасчета,
	|	СвойстваНачисленийГосударственныхСлужащих.ОтношениеКДенежномуСодержанию КАК ОтношениеКДенежномуСодержанию,
	|	Начисления.СтатьяФинансирования,
	|	Начисления.Сумма КАК Результат,
	|	Начисления.Регистратор,
	|	Начисления.НомерСтроки
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваНачисленийГосударственныхСлужащих КАК СвойстваНачисленийГосударственныхСлужащих
	|		ПО СвойстваНачисленийГосударственныхСлужащих.Начисление = Начисления.НачислениеУдержание	
	|ГДЕ
	|	Начисления.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Начисления.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено)
	|	И Начисления.Организация = &Организация
	|	И Начисления.СтатьяРасходов В(&КОСГУ)
	|	И Начисления.Сумма <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНачислений.Сотрудник КАК Сотрудник,
	|	ТаблицаНачислений.Период КАК Период,
	|	МАКСИМУМ(ВТДвиженияРаботников.Период) КАК ПериодДвижения
	|ПОМЕСТИТЬ ВТДвиженияНачисления
	|ИЗ
	|	ВТНачисления КАК ТаблицаНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияРаботников КАК ВТДвиженияРаботников
	|		ПО ТаблицаНачислений.Сотрудник = ВТДвиженияРаботников.Сотрудник
	|			И (ВТДвиженияРаботников.Период <= ТаблицаНачислений.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНачислений.Сотрудник,
	|	ТаблицаНачислений.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНачислений.СтатьяФинансирования, ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)) КАК КБК,
	|	ТаблицаНачислений.Сотрудник,
	|	ТаблицаНачислений.ВидРасчета,
	|	ТаблицаНачислений.ОтношениеКДенежномуСодержанию КАК ОтношениеКДенежномуСодержанию,	
	|	ЕСТЬNULL(ВТДвиженияРаботников.КодКатегорииПерсонала, """") КАК КодКатегорииПерсонала,
	|	ЕСТЬNULL(ВТДвиженияРаботников.ГруппаДолжностиГосударственнойСлужбы, ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.ПустаяСсылка)) КАК ГруппаДолжности,
	|	СУММА(ТаблицаНачислений.Результат + ЕСТЬNULL(ВТТаблицаРаспределения.Результат, 0)) КАК Результат
	|ПОМЕСТИТЬ ВТДанныеУчета
	|ИЗ
	|	ВТНачисления КАК ТаблицаНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияНачисления КАК ВТДвиженияНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияРаботников КАК ВТДвиженияРаботников
	|			ПО ВТДвиженияНачисления.Сотрудник = ВТДвиженияРаботников.Сотрудник
	|				И ВТДвиженияНачисления.ПериодДвижения = ВТДвиженияРаботников.Период
	|		ПО ТаблицаНачислений.Сотрудник = ВТДвиженияНачисления.Сотрудник
	|			И ТаблицаНачислений.Период = ВТДвиженияНачисления.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаРаспределения КАК ВТТаблицаРаспределения
	|		ПО ТаблицаНачислений.Регистратор = ВТТаблицаРаспределения.Регистратор
	|			И ТаблицаНачислений.НомерСтроки = ВТТаблицаРаспределения.НомерСтроки
	|			И (ВТДвиженияРаботников.КодКатегорииПерсонала В (&МуниципальныеДолжности))
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТДвиженияРаботников.КодКатегорииПерсонала В (&МуниципальныеДолжности)
	|					И ТаблицаНачислений.ВидРасчета В (&МассивВРДляРаспределения)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДвиженияРаботников.ГруппаДолжностиГосударственнойСлужбы,
	|	ТаблицаНачислений.ОтношениеКДенежномуСодержанию,
	|	ВТДвиженияРаботников.КодКатегорииПерсонала,
	|	ТаблицаНачислений.СтатьяФинансирования,
	|	ТаблицаНачислений.Сотрудник,
	|	ТаблицаНачислений.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеУчета.КБК,
	|	СУММА(ДанныеУчета.Раздел1_СТР011 + ДанныеУчета.Раздел1_СТР012) КАК Раздел1_СТР010,
	|	СУММА(ДанныеУчета.Раздел1_СТР011) КАК Раздел1_СТР011,
	|	СУММА(ДанныеУчета.Раздел1_СТР012) КАК Раздел1_СТР012,
	|	СУММА(ДанныеУчета.Раздел1_СТР021 + ДанныеУчета.Раздел1_СТР022 + ДанныеУчета.Раздел1_СТР024) КАК Раздел1_СТР020,
	|	СУММА(ДанныеУчета.Раздел1_СТР021) КАК Раздел1_СТР021,
	|	СУММА(ДанныеУчета.Раздел1_СТР022) КАК Раздел1_СТР022,
	|	СУММА(ДанныеУчета.Раздел1_СТР023) КАК Раздел1_СТР023,
	|	СУММА(ДанныеУчета.Раздел1_СТР024) КАК Раздел1_СТР024,
	|	СУММА(ДанныеУчета.Раздел1_СТР030) КАК Раздел1_СТР030,
	|	СУММА(ДанныеУчета.Раздел1_СТР040) КАК Раздел1_СТР040,
	|	СУММА(ДанныеУчета.Раздел1_СТР011 + ДанныеУчета.Раздел1_СТР012 + ДанныеУчета.Раздел1_СТР021 + ДанныеУчета.Раздел1_СТР022 + ДанныеУчета.Раздел1_СТР024 + ДанныеУчета.Раздел1_СТР030 + ДанныеУчета.Раздел1_СТР040) КАК Раздел1_СТР050,
	|	СУММА(ДанныеУчета.Раздел3_СТР410 + ДанныеУчета.Раздел3_СТР420 + ДанныеУчета.Раздел3_СТР430 + ДанныеУчета.Раздел3_СТР440 + ДанныеУчета.Раздел3_СТР450) КАК Раздел3_СТР400,
	|	СУММА(ДанныеУчета.Раздел3_СТР410) КАК Раздел3_СТР410,
	|	СУММА(ДанныеУчета.Раздел3_СТР420) КАК Раздел3_СТР420,
	|	СУММА(ДанныеУчета.Раздел3_СТР430) КАК Раздел3_СТР430,
	|	СУММА(ДанныеУчета.Раздел3_СТР440) КАК Раздел3_СТР440,
	|	СУММА(ДанныеУчета.Раздел3_СТР450) КАК Раздел3_СТР450
	|ИЗ
	|	(ВЫБРАТЬ
	|		Данные.КБК КАК КБК,
	|		СУММА(ВЫБОР
	|				КОГДА  Данные.КодКатегорииПерсонала В (""МуниципальнаяДолжность"")
	|						И (НЕ Данные.ОтношениеКДенежномуСодержанию В (&Строка011Исключения))
	|						И Данные.ОтношениеКДенежномуСодержанию <> ЗНАЧЕНИЕ(Перечисление.ОтношениеНачисленийКДенежномуСодержанию.ПустаяСсылка)
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел1_СТР011,
	|		СУММА(ВЫБОР
	|				КОГДА  Данные.КодКатегорииПерсонала В (""МуниципальнаяДолжность"")
	|						И (Данные.ОтношениеКДенежномуСодержанию В (&Строка012)
	|							ИЛИ Данные.ОтношениеКДенежномуСодержанию = ЗНАЧЕНИЕ(Перечисление.ОтношениеНачисленийКДенежномуСодержанию.ПустаяСсылка))
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел1_СТР012,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""ДолжностьМуниципальнойСлужбы"")
	|						И Данные.ОтношениеКДенежномуСодержанию В (&Строка021)
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел1_СТР021,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""ДолжностьМуниципальнойСлужбы"")
	|						И (НЕ Данные.ОтношениеКДенежномуСодержанию В (&Строка022Исключения))
	|						И Данные.ОтношениеКДенежномуСодержанию <> ЗНАЧЕНИЕ(Перечисление.ОтношениеНачисленийКДенежномуСодержанию.ПустаяСсылка)
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел1_СТР022,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""ДолжностьМуниципальнойСлужбы"")
	|						И Данные.ОтношениеКДенежномуСодержанию В (&Строка023)
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел1_СТР023,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""ДолжностьМуниципальнойСлужбы"")
	|						И Данные.ОтношениеКДенежномуСодержанию В (&Строка024)
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел1_СТР024,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""НеЯвляетсяДолжностьюМуниципальнойСлужбы"")
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел1_СТР030,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""МуниципальнаяДолжностьНовойСистемыОплатыТруда"")
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел1_СТР040,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""ДолжностьМуниципальнойСлужбы"")
	|						И (НЕ Данные.ОтношениеКДенежномуСодержанию В (&Строка024))
	|						И Данные.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Высшие)
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел3_СТР410,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""ДолжностьМуниципальнойСлужбы"")
	|						И (НЕ Данные.ОтношениеКДенежномуСодержанию В (&Строка024))
	|						И Данные.ГруппаДолжности =  ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Главные)
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел3_СТР420,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""ДолжностьМуниципальнойСлужбы"")
	|						И (НЕ Данные.ОтношениеКДенежномуСодержанию В (&Строка024))
	|						И Данные.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Ведущие)
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел3_СТР430,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""ДолжностьМуниципальнойСлужбы"")
	|						И (НЕ Данные.ОтношениеКДенежномуСодержанию В (&Строка024))
	|						И Данные.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Старшие)
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел3_СТР440,
	|		СУММА(ВЫБОР
	|				КОГДА Данные.КодКатегорииПерсонала В (""ДолжностьМуниципальнойСлужбы"")
	|						И (НЕ Данные.ОтношениеКДенежномуСодержанию В (&Строка024))
	|						И Данные.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Младшие)
	|					ТОГДА Данные.Результат
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК Раздел3_СТР450
	|	ИЗ
	|		ВТДанныеУчета КАК Данные
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Данные.КБК) КАК ДанныеУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчета.КБК";

	
	Строка011Исключения = Новый СписокЗначений;
	Строка011Исключения.Добавить(Перечисления.ОтношениеНачисленийКДенежномуСодержанию.ПрочиеВыплаты);
	Запрос.УстановитьПараметр("Строка011Исключения", Строка011Исключения);
	
 	Строка012 = Новый СписокЗначений;
	Строка012.Добавить(Перечисления.ОтношениеНачисленийКДенежномуСодержанию.ПрочиеВыплаты);
	Запрос.УстановитьПараметр("Строка012", Строка012);
	
 	Строка021 = Новый СписокЗначений;
	Строка021.Добавить(Перечисления.ОтношениеНачисленийКДенежномуСодержанию.ДолжностнойОклад);
	Запрос.УстановитьПараметр("Строка021", Строка021);
	
 	Строка022Исключения = Новый СписокЗначений;
	Строка022Исключения.Добавить(Перечисления.ОтношениеНачисленийКДенежномуСодержанию.ДолжностнойОклад);
	Строка022Исключения.Добавить(Перечисления.ОтношениеНачисленийКДенежномуСодержанию.ПрочиеВыплаты);
	Запрос.УстановитьПараметр("Строка022Исключения", Строка022Исключения);
	
 	Строка023 = Новый СписокЗначений;
	Строка023.Добавить(Перечисления.ОтношениеНачисленийКДенежномуСодержанию.ЕжемесячноеДенежноеПоощрение);
	Запрос.УстановитьПараметр("Строка023", Строка023);
	
 	Строка024 = Новый СписокЗначений;
	Строка024.Добавить(Перечисления.ОтношениеНачисленийКДенежномуСодержанию.ПрочиеВыплаты);
	Строка024.Добавить(Перечисления.ОтношениеНачисленийКДенежномуСодержанию.ПустаяСсылка());
	Запрос.УстановитьПараметр("Строка024", Строка024);
			
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДанных.Расходы = РезультатЗапроса.Выбрать();
	
	СтруктураДанных.Справка = СтруктураДанных.Расходы;
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция ПолучитьДанныеПоФорме14МОЧисленность(Организация, ДатаНачала, ДатаОкончания) экспорт 
	
	ТаблицаДанныхОтчета = Новый ТаблицаЗначений;
	ТаблицаДанныхОтчета.Колонки.Добавить("Должность", Новый ОписаниеТипов("СправочникСсылка.Должности"));
	ТаблицаДанныхОтчета.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаДанныхОтчета.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ТаблицаДанныхОтчета.Колонки.Добавить("КодКатегорииПерсонала", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
	ТаблицаДанныхОтчета.Колонки.Добавить("ГруппаДолжности", Новый ОписаниеТипов("ПеречислениеСсылка.ГруппыДолжностейГосударственнойСлужбы"));
	ТаблицаДанныхОтчета.Колонки.Добавить("КоличествоСтавок", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("КоличествоЗанятыхСтавок", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("СписочнаяЧисленность", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("СписочнаяЧисленностьНаКонецПериода", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("СреднесписочнаяЧисленность", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("СреднесписочнаяЧисленностьЗаПериод", Новый ОписаниеТипов("Число"));
		
	СтруктураЧисленности = СтатистикаПерсоналаРасширенный.СведенияОДолжностяхИЧисленностиРегламентированнойОтчетности(Организация, ДатаНачала, ДатаОкончания);
	
	
	СтрокаОрганизации = СтруктураЧисленности.ДанныеШтатногоРасписания.Строки.Найти(Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		Для Каждого СтрокаЧисленности Из СтрокаОрганизации.Строки Цикл
	   		НоваяСтрокаОтчета = ТаблицаДанныхОтчета.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаОтчета,СтрокаЧисленности);
			НоваяСтрокаОтчета.КодКатегорииПерсонала = СтрокаЧисленности.Должность_СтрокаОтчетностиФормы14_КодКатегорииПерсонала;
			НоваяСтрокаОтчета.ГруппаДолжности 		= СтрокаЧисленности.Должность_ГруппаДолжности_;
		КонецЦикла;	
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("Подразделение,Должность");
	СтрокаОрганизации = СтруктураЧисленности.ДанныеСреднесписочнойЧисленности.Строки.Найти(Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		Для Каждого СтрокаЧисленности Из СтрокаОрганизации.Строки Цикл
			ЗаполнитьЗначенияСвойств(СтруктураПоиска,СтрокаЧисленности);
			ПозицииШтатногоРасписания = ТаблицаДанныхОтчета.НайтиСтроки(СтруктураПоиска);
			Если ПозицииШтатногоРасписания.Количество() > 0 Тогда
				СтрокаШР = ПозицииШтатногоРасписания[0];
				СтрокаШР.СписочнаяЧисленность 				= СтрокаШР.СписочнаяЧисленность + СтрокаЧисленности.ЧисленностьСписочногоСостава;
				СтрокаШР.СписочнаяЧисленностьНаКонецПериода = СтрокаШР.СписочнаяЧисленностьНаКонецПериода + СтрокаЧисленности.ЧисленностьНаКонецПериодаСписочногоСостава;
				СтрокаШР.СреднесписочнаяЧисленность 		= СтрокаШР.СреднесписочнаяЧисленность + СтрокаЧисленности.СредняяЧисленностьСписочногоСостава;
				СтрокаШР.СреднесписочнаяЧисленностьЗаПериод = СтрокаШР.СреднесписочнаяЧисленностьЗаПериод + СтрокаЧисленности.СредняяЧисленностьЗаПериодСписочногоСостава;
			Иначе
				СтрокаШР = ТаблицаДанныхОтчета.Добавить();	
				ЗаполнитьЗначенияСвойств(СтрокаШР,СтрокаЧисленности);
				СтрокаШР.КодКатегорииПерсонала 	= СтрокаЧисленности.Должность_СтрокаОтчетностиФормы14_КодКатегорииПерсонала;
				СтрокаШР.ГруппаДолжности 		= СтрокаЧисленности.Должность_ГруппаДолжности_;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
	ТаблицаДанныхОтчета.Свернуть("СтатьяФинансирования,КодКатегорииПерсонала,ГруппаДолжности","КоличествоСтавок,СписочнаяЧисленностьНаКонецПериода,СреднесписочнаяЧисленностьЗаПериод,СреднесписочнаяЧисленность,СреднесписочнаяЧисленностьЗаПериод");
	
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТаблицаДанныхОтчета", ТаблицаДанныхОтчета);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДанныхОтчета.СтатьяФинансирования,
	|	ТаблицаДанныхОтчета.КодКатегорииПерсонала,
	|	ТаблицаДанныхОтчета.ГруппаДолжности,
	|	ТаблицаДанныхОтчета.КоличествоСтавок,
	|	ТаблицаДанныхОтчета.СписочнаяЧисленностьНаКонецПериода,
	|	ТаблицаДанныхОтчета.СреднесписочнаяЧисленность КАК Среднесписочная
	|ПОМЕСТИТЬ ВТТаблицаДанныхОтчета
	|ИЗ &ТаблицаДанныхОтчета КАК ТаблицаДанныхОтчета
	|;	
	|
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжность""
	|					ИЛИ ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					ИЛИ ДолжностиОрганизаций.КодКатегорииПерсонала = ""НеЯвляетсяДолжностьюМуниципальнойСлужбы""
	|					ИЛИ ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжностьНовойСистемыОплатыТруда""
	|				ТОГДА ДолжностиОрганизаций.КоличествоСтавок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР290Гр1,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжность""
	|				ТОГДА ДолжностиОрганизаций.КоличествоСтавок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР200Гр1,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|				ТОГДА ДолжностиОрганизаций.КоличествоСтавок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР210Гр1,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Высшие)
	|				ТОГДА ДолжностиОрганизаций.КоличествоСтавок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР220Гр1,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Главные)
	|				ТОГДА ДолжностиОрганизаций.КоличествоСтавок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР230Гр1,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Ведущие)
	|				ТОГДА ДолжностиОрганизаций.КоличествоСтавок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР240Гр1,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Старшие)
	|				ТОГДА ДолжностиОрганизаций.КоличествоСтавок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР250Гр1,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Младшие)
	|				ТОГДА ДолжностиОрганизаций.КоличествоСтавок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР260Гр1,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""НеЯвляетсяДолжностьюМуниципальнойСлужбы""
	|				ТОГДА ДолжностиОрганизаций.КоличествоСтавок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР270Гр1,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжностьНовойСистемыОплатыТруда""
	|				ТОГДА ДолжностиОрганизаций.КоличествоСтавок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР280Гр1,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжность""
	|					ИЛИ ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					ИЛИ ДолжностиОрганизаций.КодКатегорииПерсонала = ""НеЯвляетсяДолжностьюМуниципальнойСлужбы""
	|					ИЛИ ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжностьНовойСистемыОплатыТруда""
	|				ТОГДА ДолжностиОрганизаций.СписочнаяЧисленностьНаКонецПериода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР290Гр2,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжность""
	|				ТОГДА ДолжностиОрганизаций.СписочнаяЧисленностьНаКонецПериода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР200Гр2,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|				ТОГДА ДолжностиОрганизаций.СписочнаяЧисленностьНаКонецПериода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР210Гр2,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Высшие)
	|				ТОГДА ДолжностиОрганизаций.СписочнаяЧисленностьНаКонецПериода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР220Гр2,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Главные)
	|				ТОГДА ДолжностиОрганизаций.СписочнаяЧисленностьНаКонецПериода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР230Гр2,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Ведущие)
	|				ТОГДА ДолжностиОрганизаций.СписочнаяЧисленностьНаКонецПериода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР240Гр2,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Старшие)
	|				ТОГДА ДолжностиОрганизаций.СписочнаяЧисленностьНаКонецПериода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР250Гр2,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Младшие)
	|				ТОГДА ДолжностиОрганизаций.СписочнаяЧисленностьНаКонецПериода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР260Гр2,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""НеЯвляетсяДолжностьюМуниципальнойСлужбы""
	|				ТОГДА ДолжностиОрганизаций.СписочнаяЧисленностьНаКонецПериода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР270Гр2,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжностьНовойСистемыОплатыТруда""
	|				ТОГДА ДолжностиОрганизаций.СписочнаяЧисленностьНаКонецПериода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР280Гр2,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжность""
	|					ИЛИ ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					ИЛИ ДолжностиОрганизаций.КодКатегорииПерсонала = ""НеЯвляетсяДолжностьюМуниципальнойСлужбы""
	|					ИЛИ ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжностьНовойСистемыОплатыТруда""
	|				ТОГДА ДолжностиОрганизаций.Среднесписочная
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР290Гр3,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжность""
	|				ТОГДА ДолжностиОрганизаций.Среднесписочная
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР200Гр3,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|				ТОГДА ДолжностиОрганизаций.Среднесписочная
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР210Гр3,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Высшие)
	|				ТОГДА ДолжностиОрганизаций.Среднесписочная
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР220Гр3,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Главные)
	|				ТОГДА ДолжностиОрганизаций.Среднесписочная
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР230Гр3,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Ведущие)
	|				ТОГДА ДолжностиОрганизаций.Среднесписочная
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР240Гр3,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Старшие)
	|				ТОГДА ДолжностиОрганизаций.Среднесписочная
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР250Гр3,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""ДолжностьМуниципальнойСлужбы""
	|					И ДолжностиОрганизаций.ГруппаДолжности = ЗНАЧЕНИЕ(Перечисление.ГруппыДолжностейГосударственнойСлужбы.Младшие)
	|				ТОГДА ДолжностиОрганизаций.Среднесписочная
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР260Гр3,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""НеЯвляетсяДолжностьюМуниципальнойСлужбы""
	|				ТОГДА ДолжностиОрганизаций.Среднесписочная
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР270Гр3,
	|	СУММА(ВЫБОР
	|			КОГДА ДолжностиОрганизаций.КодКатегорииПерсонала = ""МуниципальнаяДолжностьНовойСистемыОплатыТруда""
	|				ТОГДА ДолжностиОрганизаций.Среднесписочная
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Раздел2_СТР280Гр3,	
	|	ЕСТЬNULL(ДолжностиОрганизаций.СтатьяФинансирования, ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)) КАК КБК
	|ИЗ
	|	ВТТаблицаДанныхОтчета КАК ДолжностиОрганизаций
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(ДолжностиОрганизаций.СтатьяФинансирования, ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка))";
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	Возврат Результат.Выбрать();
	
КонецФункции

Процедура СформироватьОтчетФорма14МО(ТаблицаОтчета, Организация, ДатаНачала, ДатаОкончания)
	
	Точность = 4;
	
	Если ДатаОкончания >= ДатаПереходаНа259н() Тогда
		Макет = ПолучитьМакет("Форма14МО_по_259н")
	ИначеЕсли ДатаОкончания >= ДатаПереходаНа86н() Тогда
		Макет = ПолучитьМакет("Форма14МО_по_86н")
	ИначеЕсли ДатаОкончания >= ДатаПереходаНа143н() Тогда
		Макет = ПолучитьМакет("Форма14МО_по_143н")
	ИначеЕсли ДатаОкончания >= ДатаПереходаНа179н() Тогда
		Макет = ПолучитьМакет("Форма14МО_по_179н")
	Иначе	
		Макет = ПолучитьМакет("Форма14МО")
	КонецЕсли;
	
		
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Если Точность = 1 Тогда
		Шапка.Параметры.ЕдиницаИзмерения = "Рубль";
	ИначеЕсли Точность = 4 Тогда
		Шапка.Параметры.ЕдиницаИзмерения = "Тысяча рублей";
	ИначеЕсли Точность = 7 Тогда
		Шапка.Параметры.ЕдиницаИзмерения = "Миллион рублей";
	КонецЕсли;
	Шапка.Параметры.Организация = Организация.НаименованиеПолное;
	
	ТаблицаОтчета.Вывести(Шапка);
	
	СтруктураДанных = ПолучитьДанныеПоФорме14МОРасходы(Организация, ДатаНачала, ДатаОкончания);
	
	СтруктураДанных.Расходы.Сбросить();
	ВывестиРаздел(ТаблицаОтчета, СтруктураДанных.Расходы, 1, Точность-1, Макет);	
	ТаблицаОтчета.ВывестиГоризонтальныйРазделительСтраниц();
	
	ВыборкаЧисленность = ПолучитьДанныеПоФорме14МОЧисленность(Организация, ДатаНачала, ДатаОкончания);
	ВывестиРаздел(ТаблицаОтчета, ВыборкаЧисленность, 2, 0, Макет);
	ТаблицаОтчета.ВывестиГоризонтальныйРазделительСтраниц();
	
	СтруктураДанных.Расходы.Сбросить();
	ВывестиРаздел(ТаблицаОтчета, СтруктураДанных.Расходы, 3, Точность-1, Макет);	
	ТаблицаОтчета.ВывестиГоризонтальныйРазделительСтраниц();	
	
	ТаблицаОтчета.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТаблицаОтчета.Автомасштаб        = Истина;
	ТаблицаОтчета.ПолеСлева          = 0;
	ТаблицаОтчета.ПолеСправа         = 0;
	ТаблицаОтчета.Защита             = Ложь;
	ТаблицаОтчета.ТолькоПросмотр     = Истина;	
	
КонецПроцедуры 


#КонецОбласти

#КонецЕсли