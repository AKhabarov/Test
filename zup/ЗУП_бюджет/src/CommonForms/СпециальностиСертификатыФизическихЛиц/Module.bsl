
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Отбор.Свойство("ФизическоеЛицо", ФизическоеЛицоСсылка);
	
	СотрудникиФормыРасширенный.ПрочитатьНаборЗаписей(ЭтаФорма, "СпециальностиФизическихЛиц");
	СпециальностиФизическихЛиц.Сортировать("Период");
	
	СотрудникиФормыРасширенный.ПрочитатьНаборЗаписей(ЭтаФорма, "СертификатыПоСпециальностиФизическихЛиц");
	СертификатыПоСпециальностиФизическихЛиц.Сортировать("ДатаПолучения");
	
	Элементы.СпециальностиФизическихЛицРазрядКатегория.Заголовок = РазрядыКатегорииДолжностей.ИнициализироватьЗаголовокФормыИРеквизитов("РеквизитРазрядКатегорияВКадровыхДокументах");
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПредставлениеСпециальностей = 
		РегистрыСведений.СпециальностиФизическихЛиц.ПредставлениеСпециальностейПоКоллекцииЗаписей(СпециальностиФизическихЛиц);
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ИзмененыСпециальностиФизическихЛиц", ПредставлениеСпециальностей, ВладелецФормы);
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	СотрудникиФормыРасширенный.СохранитьНаборЗаписей(ЭтаФорма, "СертификатыПоСпециальностиФизическихЛиц");	
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Набор = ДанныеФормыВЗначение(СертификатыПоСпециальностиФизическихЛиц, Тип("РегистрСведенийНаборЗаписей.СертификатыПоСпециальностиФизическихЛиц"));
	УстановитьСоответствиеОбъектаИФормы(Набор, Новый Структура("ИдентификаторФормы,ИмяРеквизита", УникальныйИдентификатор, "СертификатыПоСпециальностиФизическихЛиц")); 
	Отказ = Отказ Или Не Набор.ПроверитьЗаполнение();
	
КонецПроцедуры

&НаКлиенте
Процедура СпециальностиФизическихЛицПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ФизическоеЛицо = ФизическоеЛицоСсылка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатыФизическихЛицПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ФизическоеЛицо = ФизическоеЛицоСсылка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
