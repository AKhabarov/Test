
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Сотрудник", Сотрудник);
	Параметры.Свойство("ФизическоеЛицо", ФизическоеЛицо);
	Параметры.Свойство("ВоинскоеСпециальноеЗвание", ВоинскоеСпециальноеЗвание);
	Параметры.Свойство("ДатаПрисвоения", ДатаПрисвоения);
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
		Запрос.Текст = "ВЫБРАТЬ
		               |	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		               |	Сотрудники.ФизическоеЛицо.Наименование КАК Наименование
		               |ИЗ
		               |	Справочник.Сотрудники КАК Сотрудники
		               |ГДЕ
		               |	Сотрудники.Ссылка = &Сотрудник";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда 
			ФизическоеЛицо = Выборка.ФизическоеЛицо;
			Заголовок = Выборка.Наименование;
		КонецЕсли;
	Иначе
		Заголовок = ФизическоеЛицо;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ЗначениеЗаполнено(ВоинскоеСпециальноеЗвание) И Не ЗначениеЗаполнено(ДатаПрисвоения) Тогда 
		ТекстСообщения = НСтр("ru = 'Не указана дата присвоения.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ДатаПрисвоения");
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаПрисвоения) И Не ЗначениеЗаполнено(ВоинскоеСпециальноеЗвание) Тогда 
		ТекстСообщения = НСтр("ru = 'Не указано воинское, специальное звание.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВоинскоеСпециальноеЗвание");
		Возврат;
	КонецЕсли;
	
	СведенияОВоинскомСпециальномЗвании = Новый Структура("ВоинскоеСпециальноеЗвание, ДатаПрисвоения", ВоинскоеСпециальноеЗвание, ДатаПрисвоения);
	Оповестить("ИзмененоВоинскоеСпециальноеЗвание", СведенияОВоинскомСпециальномЗвании, ЭтотОбъект);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИзменения(Команда)
	
	Отбор = Новый Структура("ФизическоеЛицо", ФизическоеЛицо);
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	ОткрытьФорму("РегистрСведений.ВоинскиеСпециальныеЗванияФизическихЛиц.Форма.ИсторияИзменения", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
