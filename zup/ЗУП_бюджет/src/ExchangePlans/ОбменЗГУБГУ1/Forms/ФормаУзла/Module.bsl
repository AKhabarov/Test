
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Объект.ИспользоватьОтборПоОрганизациям Тогда
		РежимСинхронизацииДанных = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям";
	Иначе
		РежимСинхронизацииДанных = "СинхронизироватьВсеДанные";
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_УзелПланаОбмена");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если РежимСинхронизацииДанных = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям" Тогда
		Если Объект.Организации.Количество() = 0 Тогда
			Отказ = Истина;
			
			ТекстВопроса = НСтр("ru='Выбран вариант обмена данными ""Синхронизировать данные только по организациям"", но не выбрано ни одной организации.
								|Изменить вариант обмена данными на ""Синхронизировать все данные""?'");
			
			Оповещение = Новый ОписаниеОповещения("ОповещениеОтсутствиеОрганизаций", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбрабочтикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимСинхронизацииДанныхПриИзменении(Элемент)
	Объект.ИспользоватьОтборПоОрганизациям = (РежимСинхронизацииДанных = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям");
	
	Если Не Объект.ИспользоватьОтборПоОрганизациям Тогда
		Объект.Организации.Очистить();
	КонецЕсли;
	
	Модифицированность = Истина;
	
	УстановитьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
Процедура ОповещениеОтсутствиеОрганизаций(Результат, ДопПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Объект.ИспользоватьОтборПоОрганизациям = Ложь;
		РежимСинхронизацииДанных = "СинхронизироватьВсеДанные";
		Записать();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьЭлементов()
	Элементы.Организации.Доступность = (РежимСинхронизацииДанных = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям");
КонецПроцедуры

#КонецОбласти