#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьРасчетДенежногоСодержанияПрокуроров = ПолучитьФункциональнуюОпцию("ИспользоватьРасчетДенежногоСодержанияПрокуроров"); 
	ИспользоватьРасчетДенежногоСодержанияСудей 	= ПолучитьФункциональнуюОпцию("ИспользоватьРасчетДенежногоСодержанияСудей");
	
	ОтносятсяКСлужбе = Новый Массив;
	ОтносятсяКСлужбе.Добавить(Перечисления.ВидыДолжностейГосударственнойСлужбы.ГосударственнаяДолжность);
	ОтносятсяКСлужбе.Добавить(Перечисления.ВидыДолжностейГосударственнойСлужбы.ДолжностьГосударственнойГражданскойСлужбы);
	ОтносятсяКСлужбе.Добавить(Перечисления.ВидыДолжностейГосударственнойСлужбы.МуниципальнаяДолжность);
	ОтносятсяКСлужбе.Добавить(Перечисления.ВидыДолжностейГосударственнойСлужбы.ДолжностьМуниципальнойСлужбы);
	
	ОтноситсяКВоеннойСлужбе = Новый Массив;
	ОтноситсяКВоеннойСлужбе.Добавить(Перечисления.ВидыДолжностейГосударственнойСлужбы.ВоинскаяДолжность);
	ОтноситсяКВоеннойСлужбе.Добавить(Перечисления.ВидыДолжностейГосударственнойСлужбы.ДолжностьПравоохранительнойСлужбы);
	
	Для каждого Запись Из ЭтотОбъект Цикл
		Запись.ЯвляетсяДолжностьюГосударственнойСлужбы = ОтносятсяКСлужбе.Найти(Запись.ВидДолжностиГосударственнойСлужбы) <> Неопределено;
		Запись.ЯвляетсяДолжностьюВоеннойПравоохранительнойСлужбы = ОтноситсяКВоеннойСлужбе.Найти(Запись.ВидДолжностиГосударственнойСлужбы) <> Неопределено;
		Запись.ДолжностьПрокурораСледователяСудьи = (ИспользоватьРасчетДенежногоСодержанияПрокуроров И Запись.ДолжностьПрокурора) ИЛИ
			(ИспользоватьРасчетДенежногоСодержанияСудей И Запись.ДолжностьСудьи);
	КонецЦикла;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АвтоматическийРасчетСтажейФизическихЛиц") Тогда
		
		МодульАвтоматическийРасчетСтажейФизическихЛиц = ОбщегоНазначения.ОбщийМодуль("АвтоматическийРасчетСтажейФизическихЛиц");
		МодульАвтоматическийРасчетСтажейФизическихЛиц.ЗапомнитьСвойстваДолжностейСАвтоматическимРасчетомСтажей(
			ЭтотОбъект, Отказ, "ЯвляетсяДолжностьюГосударственнойСлужбы,ЯвляетсяДолжностьюВоеннойПравоохранительнойСлужбы");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АвтоматическийРасчетСтажейФизическихЛиц") Тогда
		
		МодульАвтоматическийРасчетСтажейФизическихЛиц = ОбщегоНазначения.ОбщийМодуль("АвтоматическийРасчетСтажейФизическихЛиц");
		МодульАвтоматическийРасчетСтажейФизическихЛиц.ЗаполнитьЗависимыеСтажиДолжностей(ЭтотОбъект, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
