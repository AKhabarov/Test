#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

// Устанавливает настройки соответствия показателей начислений показателям тарификации.
//
Процедура ОбновитьДанныеРегистра() Экспорт
	
	СоответствиеПоказателей = Новый Соответствие;
	
	СоответствиеПоказателей.Вставить("ПроцентНадбавкиЗаВыслугуЛет",		Перечисления.ПоказателиТарификацииМедРаботников.ПроцентНадбавкиЗаВыслугуЛет);	
	СоответствиеПоказателей.Вставить("ПроцентНадбавкиЗаВредность",		Перечисления.ПоказателиТарификацииМедРаботников.ПроцентНадбавкиЗаУсловияТруда);	
	СоответствиеПоказателей.Вставить("ПроцентНадбавкиЗаКатегорию",		Перечисления.ПоказателиТарификацииМедРаботников.ПроцентНадбавкиЗаКатегорию);	
	СоответствиеПоказателей.Вставить("ПроцентНадбавкиЗаУченуюСтепень",	Перечисления.ПоказателиТарификацииМедРаботников.ПроцентНадбавкиЗаУченуюСтепень);	
	СоответствиеПоказателей.Вставить("ПроцентНадбавкиЗаПочетноеЗвание",	Перечисления.ПоказателиТарификацииМедРаботников.ПроцентНадбавкиЗаПочетноеЗвание);	
	СоответствиеПоказателей.Вставить("ПроцентНадбавкиЗаЕзду",			Перечисления.ПоказателиТарификацииМедРаботников.ПовышениеЗаЕзду);	
	СоответствиеПоказателей.Вставить("ПроцентСниженияОклада",			Перечисления.ПоказателиТарификацииМедРаботников.ПроцентСниженияОклада);	
	
	НаборЗаписей = РегистрыСведений.НастройкаПоказателейТарификацииМедРаботников.СоздатьНаборЗаписей();

	Для каждого Элемент Из СоответствиеПоказателей Цикл
		
		ПоказательСсылка = ЗарплатаКадрыРасширенный.ПоказательПоИдентификатору(Элемент.Ключ);
		
		Если ЗначениеЗаполнено(ПоказательСсылка) Тогда
			
			НаборЗаписей.Отбор.Показатель.Установить(ПоказательСсылка);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() = 0 Тогда
								
				Запись = НаборЗаписей.Добавить();
				Запись.Показатель = ПоказательСсылка;
				Запись.ПоказательТарификации = Элемент.Значение;
 
				НаборЗаписей.ОбменДанными.Загрузка = Истина;
				НаборЗаписей.Записать();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
