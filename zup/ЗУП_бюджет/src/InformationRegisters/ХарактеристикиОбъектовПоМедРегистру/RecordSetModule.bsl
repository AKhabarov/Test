#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПодключаемыеХарактеристикиДолжностей();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьПодключаемыеХарактеристикиДолжностей()
	
	ОтборОбъект = ?(Отбор.Объект.Использование, Отбор.Объект.Значение, Неопределено);
	Если ЗначениеЗаполнено(ОтборОбъект) И ТипЗнч(ОтборОбъект) <> Тип("СправочникСсылка.Должности") Тогда
		Возврат;
	КонецЕсли;
	
	
	ХарактеристикиДолжности = РегистрыСведений.ХарактеристикиОбъектовПоМедРегистру.ХарактеристикиОбъектаМетаданныхДляРегистра(Тип("СправочникСсылка.Должности"));
	ПредставленияХарактеристик = Новый Соответствие;
	Для Каждого ХарактеристикаДолжности Из ХарактеристикиДолжности Цикл
		ПредставленияХарактеристик.Вставить(ХарактеристикаДолжности.Классификатор, ХарактеристикаДолжности.ПредставлениеХарактеристики);
	КонецЦикла;
	
	ОтборКлассификатор = ?(Отбор.Классификатор.Использование, Отбор.Классификатор .Значение, Неопределено);
	Если ЗначениеЗаполнено(ОтборКлассификатор) И ПредставленияХарактеристик[ОтборКлассификатор] = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ОтборОбъект) Или ЗначениеЗаполнено(ОтборКлассификатор) Тогда
		
		ПодключаемыеХарактеристики = ЭтотОбъект.Выгрузить();
		ПодключаемыеХарактеристики.Колонки.Добавить("Свойство", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100)));
		Для Каждого ХарактеристикаДолжности Из ПодключаемыеХарактеристики Цикл
			ХарактеристикаДолжности.Свойство = ПредставленияХарактеристик[ХарактеристикаДолжности.Классификатор];
		КонецЦикла;
		
		ПодключаемыеХарактеристики.Колонки.Удалить("Классификатор");
		ПодключаемыеХарактеристики.Колонки.Характеристика.Имя = "Значение";
		
		Если ЗначениеЗаполнено(ОтборКлассификатор) Тогда
			// дополним таблицу недостающими характеристиками
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПредставленияХарактеристик.Классификатор,
			|	ПредставленияХарактеристик.ПредставлениеХарактеристики КАК ПредставлениеКлассификатора
			|ПОМЕСТИТЬ ВТПредставленияХарактеристик
			|ИЗ
			|	&ПредставленияХарактеристик КАК ПредставленияХарактеристик
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТ.Объект,
			|	ВТ.Свойство,
			|	ВТ.Значение
			|ПОМЕСТИТЬ ВТПодключаемыеХарактеристики
			|ИЗ
			|	&ПодключаемыеХарактеристики КАК ВТ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ХарактеристикиОбъектовПоМедРегистру.Объект,
			|	ХарактеристикиОбъектовПоМедРегистру.Классификатор,
			|	ХарактеристикиОбъектовПоМедРегистру.Характеристика
			|ПОМЕСТИТЬ ВТНаборЗаписей
			|ИЗ
			|	РегистрСведений.ХарактеристикиОбъектовПоМедРегистру КАК ХарактеристикиОбъектовПоМедРегистру
			|ГДЕ
			|	ХарактеристикиОбъектовПоМедРегистру.Объект В
			|			(ВЫБРАТЬ
			|				Отбор.Объект
			|			ИЗ
			|				ВТПодключаемыеХарактеристики КАК Отбор)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТНаборЗаписей.Объект,
			|	ВТПредставленияХарактеристик.ПредставлениеКлассификатора КАК Свойство,
			|	ВТНаборЗаписей.Характеристика КАК Значение,
			|	1 КАК Индекс
			|ПОМЕСТИТЬ ВТВсеЗаписи
			|ИЗ
			|	ВТНаборЗаписей КАК ВТНаборЗаписей
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПредставленияХарактеристик КАК ВТПредставленияХарактеристик
			|		ПО ВТНаборЗаписей.Классификатор = ВТПредставленияХарактеристик.Классификатор
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВТПодключаемыеХарактеристики.Объект,
			|	ВТПодключаемыеХарактеристики.Свойство,
			|	ВТПодключаемыеХарактеристики.Значение,
			|	0
			|ИЗ
			|	ВТПодключаемыеХарактеристики КАК ВТПодключаемыеХарактеристики
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТВсеЗаписи.Объект,
			|	ВТВсеЗаписи.Свойство,
			|	МИНИМУМ(ВТВсеЗаписи.Индекс) КАК Индекс
			|ПОМЕСТИТЬ ВТИндексы
			|ИЗ
			|	ВТВсеЗаписи КАК ВТВсеЗаписи
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТВсеЗаписи.Объект,
			|	ВТВсеЗаписи.Свойство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТВсеЗаписи.Объект,
			|	ВТВсеЗаписи.Свойство,
			|	ВТВсеЗаписи.Значение
			|ИЗ
			|	ВТВсеЗаписи КАК ВТВсеЗаписи
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИндексы КАК ВТИндексы
			|		ПО ВТВсеЗаписи.Объект = ВТИндексы.Объект
			|			И ВТВсеЗаписи.Свойство = ВТИндексы.Свойство
			|			И ВТВсеЗаписи.Индекс = ВТИндексы.Индекс";
			Запрос.УстановитьПараметр("ПредставленияХарактеристик", ХарактеристикиДолжности);
			Запрос.УстановитьПараметр("ПодключаемыеХарактеристики", ПодключаемыеХарактеристики);
			
			ПодключаемыеХарактеристики = Запрос.Выполнить().Выгрузить();
		КонецЕсли;
		
		РегистрыСведений.ХарактеристикиОбъектовПоМедРегистру.ОбновитьПодключаемыеХарактеристики(Истина, ПодключаемыеХарактеристики, ОтборОбъект);
		
	ИначеЕсли ЭтотОбъект.Количество() = 0 Тогда
		РегистрыСведений.ХарактеристикиОбъектовПоМедРегистру.ОбновитьПодключаемыеХарактеристики(Истина, Новый ТаблицаЗначений);
		
	Иначе
		МассивОбъектов = ЭтотОбъект.ВыгрузитьКолонку("Объект");
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Объекты", МассивОбъектов);
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Должности.Ссылка
		|ИЗ
		|	Справочник.Должности КАК Должности
		|ГДЕ
		|	Должности.Ссылка В(&Объекты)";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПредставленияХарактеристик.Классификатор,
			|	ПредставленияХарактеристик.ПредставлениеХарактеристики КАК ПредставлениеКлассификатора
			|ПОМЕСТИТЬ ВТПредставленияХарактеристик
			|ИЗ
			|	&ПредставленияХарактеристик КАК ПредставленияХарактеристик
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ХарактеристикиОбъектовПоМедРегистру.Объект,
			|	ХарактеристикиОбъектовПоМедРегистру.Классификатор,
			|	ХарактеристикиОбъектовПоМедРегистру.Характеристика
			|ПОМЕСТИТЬ ВТНаборЗаписей
			|ИЗ
			|	&НаборЗаписей КАК ХарактеристикиОбъектовПоМедРегистру
			|ГДЕ
			|	ТИПЗНАЧЕНИЯ(ХарактеристикиОбъектовПоМедРегистру.Объект) = ТИП(Справочник.Должности)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТНаборЗаписей.Объект,
			|	ВТПредставленияХарактеристик.ПредставлениеКлассификатора КАК Свойство,
			|	ВТНаборЗаписей.Характеристика КАК Значение
			|ИЗ
			|	ВТНаборЗаписей КАК ВТНаборЗаписей
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПредставленияХарактеристик КАК ВТПредставленияХарактеристик
			|		ПО ВТНаборЗаписей.Классификатор = ВТПредставленияХарактеристик.Классификатор";
			Запрос.УстановитьПараметр("ПредставленияХарактеристик", ХарактеристикиДолжности);
			Запрос.УстановитьПараметр("НаборЗаписей", ЭтотОбъект);
			
			РегистрыСведений.ХарактеристикиОбъектовПоМедРегистру.ОбновитьПодключаемыеХарактеристики(Истина,  Запрос.Выполнить().Выгрузить());
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
	
#КонецЕсли
