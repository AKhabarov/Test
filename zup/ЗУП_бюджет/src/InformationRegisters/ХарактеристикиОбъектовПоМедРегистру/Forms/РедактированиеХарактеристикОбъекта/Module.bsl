
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриПолученииДанныхНаСервере();	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.ОбъектВладелец = Параметры.Объект;

    ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗагруженыКлассификаторыМедРегистра" Тогда
		ОбновитьНаборЗаписейНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("КлассифицированОбъектМетаданныхПоМедРегистру", ОбъектВладелец);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыНаборЗаписей

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	Если Не ЗначениеЗаполнено(ОбъектВладелец) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаХарактеристик = РегистрыСведений.ХарактеристикиОбъектовПоМедРегистру.ХарактеристикиОбъектаМетаданныхДляРегистра(ТипЗнч(ОбъектВладелец));
	
	Если НаборЗаписей.Количество() <> ТаблицаХарактеристик.Количество() Тогда
		ЭтаФорма.НаборЗаписей.Загрузить( РегистрыСведений.ХарактеристикиОбъектовПоМедРегистру.ЗаполненныеХарактеристикиОбъектаМетаданных(ОбъектВладелец) );
	КонецЕсли;
	
	// Заполним представление элемента из характеристик связей объектов метаданных по мед. регистру.
	Для Каждого СтрокаНабораЗаписей Из НаборЗаписей Цикл
		
		Если ПустаяСтрока(СтрокаНабораЗаписей.ПредставлениеХарактеристики) Тогда
			
			Характеристика = ТаблицаХарактеристик.Найти(СтрокаНабораЗаписей.Классификатор, "Классификатор");
			Если Характеристика <> Неопределено Тогда
				СтрокаНабораЗаписей.ПредставлениеХарактеристики = Характеристика.ПредставлениеХарактеристики ;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаДляСортировки = НаборЗаписей.Выгрузить();
	ТаблицаДляСортировки.Сортировать("ПредставлениеХарактеристики Возр");
	НаборЗаписей.Загрузить(ТаблицаДляСортировки);

КонецПроцедуры

&НаСервере
Процедура ОбновитьНаборЗаписейНаСервере()
	
	Если Модифицированность Тогда
		
		Записать();
		Модифицированность = Ложь;
		
	КонецЕсли;
	
	НаборЗаписей.Очистить();
	
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

#КонецОбласти


