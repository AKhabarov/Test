
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ГосударственнаяСлужбаВызовСервера.ВидыДолжностейГосударственнойСлужбыОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка);
	
КонецПроцедуры
	
#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ДоступныеВидыДолжностейГосударственнойСлужбы() Экспорт
	
	ДоступныеВидыДолжностейГосударственнойСлужбы = Новый Массив;
	
	Значения = Метаданные.Перечисления.ВидыДолжностейГосударственнойСлужбы.ЗначенияПеречисления;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВоеннуюСлужбу") Тогда
		Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетДенежногоДовольствияВоеннослужащих") Тогда
			ДоступныеВидыДолжностейГосударственнойСлужбы.Добавить(Значения.ВоинскаяДолжность);
		Иначе	
		    ДоступныеВидыДолжностейГосударственнойСлужбы.Добавить(Значения.ДолжностьПравоохранительнойСлужбы);
		КонецЕсли;
		ДоступныеВидыДолжностейГосударственнойСлужбы.Добавить(Значения.НеЯвляетсяДолжностьюГосударственнойСлужбы);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГосударственнуюСлужбу") Тогда
		ДоступныеВидыДолжностейГосударственнойСлужбы.Добавить(Значения.ДолжностьГосударственнойГражданскойСлужбы);
		
		НастройкиГосударственнойСлужбы = ГосударственнаяСлужба.НастройкиГосударственнойСлужбы();
		Если НастройкиГосударственнойСлужбы.ИспользоватьГосударственныеМуниципальныеДолжности Тогда
			ДоступныеВидыДолжностейГосударственнойСлужбы.Добавить(Значения.ГосударственнаяДолжность);
		КонецЕсли;
		
		Если ДоступныеВидыДолжностейГосударственнойСлужбы.Найти(Значения.НеЯвляетсяДолжностьюГосударственнойСлужбы) = Неопределено Тогда
			ДоступныеВидыДолжностейГосударственнойСлужбы.Добавить(Значения.НеЯвляетсяДолжностьюГосударственнойСлужбы);
		КонецЕсли;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьМуниципальнуюСлужбу") Тогда
		ДоступныеВидыДолжностейГосударственнойСлужбы.Добавить(Значения.ДолжностьМуниципальнойСлужбы);
		ДоступныеВидыДолжностейГосударственнойСлужбы.Добавить(Значения.НеЯвляетсяДолжностьюМуниципальнойСлужбы);
		
		НастройкиГосударственнойСлужбы = ГосударственнаяСлужба.НастройкиГосударственнойСлужбы();
		Если НастройкиГосударственнойСлужбы.ИспользоватьГосударственныеМуниципальныеДолжности Тогда
			ДоступныеВидыДолжностейГосударственнойСлужбы.Добавить(Значения.МуниципальнаяДолжность);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДоступныеВидыДолжностейГосударственнойСлужбы;
	
КонецФункции

#КонецОбласти

#КонецЕсли